# SPDX-License-Identifier: GPL-3.0-or-later
# SPDX-FileCopyrightText: 2021 Normation SAS

bundle agent monitoring_hook_pre
{
  methods:
    "reporting" usebundle => disable_reporting;
    "deletion" usebundle => file_absent("/var/rudder/tmp/rudder_monitoring.csv");
    "reporting" usebundle => enable_reporting;
}

bundle agent monitoring_hook_post
{
  classes:
    "csv_exists" expression => fileexists("/var/rudder/tmp/rudder_monitoring.csv");

  methods:
    "reporting" usebundle => disable_reporting;
    "reporting" usebundle => _method_reporting_context("Monitoring", "None");

    csv_exists::
    "share file" usebundle => sharedfile_to_node("root", "rudder_monitoring.csv", "/var/rudder/tmp/rudder_monitoring.csv", "1d");

    any::
    "reporting" usebundle => enable_reporting;

    csv_exists::
    "reporting" usebundle => rudder_common_reports_generic("Common", "sharedfile_to_node_rudder_monitoring_csv", "${system_common.directiveId}", "Monitoring", "None", "Share Rudder monitoring information with the server");

    !csv_exists::
    "reporting" usebundle => rudder_common_report("Common", "result_na", "${system_common.directiveId}", "Monitoring", "None", "No Rudder monitoring information to share with the server");
}
