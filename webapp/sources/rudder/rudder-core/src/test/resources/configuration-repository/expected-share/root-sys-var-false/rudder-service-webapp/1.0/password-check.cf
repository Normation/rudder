# SPDX-License-Identifier: GPL-3.0-or-later
# SPDX-FileCopyrightText: 2021 Normation SAS

bundle agent system_rudder_webapp_properties {
  vars:
      "password_file"   string => "${g.rudder_base}/etc/rudder-passwords.conf";
      "properties_file"   string => "${g.rudder_base}/etc/rudder-web.properties";
      "c_properties_file" string => canonify("${properties_file}");
      "user_file"         string => "${g.rudder_base}/etc/rudder-users.xml";

      "secure_files"       slist => { "${password_file}", "${properties_file}", "${user_file}" };

      "pwd1"                 int => getfields("RUDDER_PSQL_PASSWORD:.*","${password_file}",":","psql_password");
      "pwd2"                 int => getfields("RUDDER_OPENLDAP_BIND_PASSWORD:.*","${password_file}",":","ldap_password");
      "pwd3"                 int => getfields("RUDDER_WEBDAV_PASSWORD:.*","${password_file}",":","dav_password");

  classes:
    # if configuration file has been updated, we need to restart rudder-jetty
      "rudder_server_system_restart_jetty" expression => "file_key_value_present_${c_properties_file}_repaired",
                                                scope => "namespace";

  methods:
      "any" usebundle => _method_reporting_context("Check webapp configuration", "rudder-passwords.conf permissions");
      "any" usebundle => permissions("${password_file}", "600", "root", "0");
      "any" usebundle => _method_reporting_context("Check webapp configuration", "rudder-web.properties permissions");
      "any" usebundle => permissions("${properties_file}", "600", "root", "0");
      "any" usebundle => _method_reporting_context("Check webapp configuration", "rudder-users.xml permissions");
      "any" usebundle => permissions("${user_file}", "600", "root", "0");

      "any" usebundle => _method_reporting_context("Check webapp configuration", "postgres password");
      "any" usebundle => file_key_value_present("${properties_file}", "rudder.jdbc.password", "${psql_password[2]}", "=");

      "any" usebundle => _method_reporting_context("Check webapp configuration", "ldap password");
      "any" usebundle => file_key_value_present("${properties_file}", "ldap.authpw", "${ldap_password[2]}", "=");

      "any" usebundle => _method_reporting_context("Check webapp configuration", "webdav password");
      "any" usebundle => file_key_value_present("${properties_file}", "rudder.webdav.password", "${dav_password[2]}", "=");

}
