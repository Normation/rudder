#######################################################
#
# This is a sample test file to copy when creating a
# new test. Please keep it updated with best practices.
#
#######################################################

bundle common acc_path
{
  vars:
    "root" string => getenv("NCF_TESTS_ACCEPTANCE", 1024);
}

body common control
{
      inputs => { "${acc_path.root}/default.cf.sub", "${acc_path.root}/default_ncf.cf.sub", "@{ncf_inputs.default_files}" };
      bundlesequence  => { default("$(this.promise_filename)") };
      version => "1.0";
}

#######################################################

bundle agent init
{
  vars:
    "tmp"         string => getenv("TEMP", 1024);
    "file"        string => "${tmp}/test";
    "file_canon"  string => canonify("${file}");
    "line_to_add" string => "This is a test line!";
}

#######################################################

bundle agent test
{

  methods:
    "ph1" usebundle => file_ensure_lines_present("${init.file}", "sample two"); # should get repaired
    "ph2" usebundle => file_ensure_lines_absent("${init.file}", "sample one"); # should get success
    "ph3" usebundle => file_ensure_lines_absent("/dev/zero", "sample three"); # should get failed

    "ph1"  usebundle => _classes_inverted_copy("file_ensure_lines_absent_${init.file_canon}","global_classA");
    "ph2"  usebundle => _classes_inverted_copy("file_ensure_lines_present_${init.file_canon}","global_classB");
    "ph3"  usebundle => _classes_inverted_copy("file_ensure_lines_absent__dev_zero","global_classC");

}

#######################################################

bundle agent check
{
  classes:
      "ok_success"  expression => "(global_classA_ok.global_classA_reached.global_classA_repaired.promise_repaired_global_classA)";
      "ok_repaired" expression => "(global_classB_ok.global_classB_reached.global_classB_kept.global_classB_not_repaired.promise_kept_global_classB)";
      "ok_error"    expression => "(global_classC_failed.repair_failed_global_classC.global_classC_not_ok.global_classC_not_kept.global_classC_not_repaired.global_classC_reached)";
      "ok"          expression => "ok_success.ok_repaired.ok_error";

  reports:
    ok::
      "$(this.promise_filename) Pass";
    !ok::
      "$(this.promise_filename) FAIL";
}
