#######################################################
#
# Test checking if ACLs are present or not
# 
#######################################################

bundle common acc_path
{
  vars:
    "root" string => getenv("NCF_TESTS_ACCEPTANCE", 1024);
}

body common control
{
      inputs => { "${acc_path.root}/default.cf.sub", "${acc_path.root}/default_ncf.cf.sub", "@{ncf_inputs.default_files}" };
      bundlesequence  => { configuration, default("${this.promise_filename}") };
      version => "1.0";
}

#######################################################

bundle agent init
{
  vars:
      "tmp"                  string => getenv("TEMP", 1024);
      "initial_acls"         slist => { "user:*:rwx", "group:*:rx", "user:bin:r", "group:bin:r", "all:", "mask:rwx" };
      "subfile_initial_acls" slist => { "user:*:rwx", "group:*:rx", "user:bin:rx", "group:bin:r", "all:", "mask:rwx" };
      # The tests uses "." instead in some mask pattern because it can be wrongly display in getfacl output
      # if no aces are impacted by the mask

      "files[1]" string => "file1";
      "user[1]"  string => "*:";
      "group[1]" string => "";
      "other[1]" string => "";
      "lines[1]"  slist => { "user::---", "group::r-x", "user:bin:r--", "group:bin:r--", "other::---", "mask::r.x" };

      "files[2]" string => "file2";
      "user[2]"  string => "";
      "group[2]" string => "*:";
      "other[2]" string => "";
      "lines[2]"  slist => { "user::rwx", "group::---", "user:bin:r--", "group:bin:r--", "other::---", "mask::r.." };

      "files[3]" string => "file3";
      "user[3]"  string => "bin:-rwx";
      "group[3]" string => "*:";
      "other[3]" string => "";
      "lines[3]"  slist => { "user::rwx", "user:bin:---", "group::---", "group:bin:r--", "other::---", "mask::r.." };

      "files[4]" string => "file4";
      "user[4]"  string => "bin:-r, bin:+x";
      "group[4]" string => "*:";
      "other[4]" string => "";
      "lines[4]"  slist => { "user:bin:--x", "user::rwx", "group::---", "group:bin:r--", "other::---", "mask::r.x" };

      "files[5]" string => "file5";
      "user[5]"  string => "bin:-r, root:rx";
      "group[5]" string => "*:";
      "other[5]" string => "";
      "lines[5]"  slist => { "user:bin:---", "user:root:r-x", "group::---", "user::rwx", "group::---", "group:bin:r--", "other::---", "mask::r.x" };

      "files[6]" string => "file6";
      "user[6]"  string => "bin:-r, root:rx";
      "group[6]" string => "*:";
      "other[6]" string => "-r, =rx";
      "lines[6]"  slist => {  "user:bin:---", "user:root:r-x", "group::---", "user::rwx", "group::---", "group:bin:r--", "mask::r.x", "other::r-x" };

      # To test the recursivity
      "files[7]" string => "file7/.";
      "user[7]"  string => "bin:=rx";
      "group[7]" string => "";
      "other[7]" string => "";
      "lines[7]"  slist => {  "user:bin:r-x", "user::rwx", "group::r-x", "group:bin:r--", "mask::r.x", "other::---" };

      "files[8]" string => "file7/1/2/3/4/5/subfile1";
      "user[8]"  string => "";
      "group[8]" string => "";
      "other[8]" string => "";
      "lines[8]"  slist => { "user::rwx", "group::r-x", "user:bin:r-x", "group:bin:r--", "other::---", "mask::r.x" };

      # To test regex
      "files[9]" string => "rfile1";
      "lines[9]"  slist => {  "user:bin:---", "user:root:r-x", "group::---", "user::rwx", "group::---", "group:bin:r--", "mask::r.x", "other::r-x" };

      "files[10]" string => "rfile2";
      "lines[10]"  slist => {  "user:bin:---", "user:root:r-x", "group::---", "user::rwx", "group::---", "group:bin:r--", "mask::r.x", "other::r-x" };

      "files[11]" string => "rfile*";
      "user[11]"  string => "bin:-r, root:rx";
      "group[11]" string => "*:";
      "other[11]" string => "-r, =rx";

      "indices" slist => getindices("files");
      # Do not create 11
      "create_indices" slist => { "1", "2", "3", "4", "5", "6", "7", "8", "9", "10" };
      "file_list"      slist => { "1", "2", "3", "4", "5", "6", "8", "9", "10" };
      "subfile_list"   slist => { "7" };

      "printable_lines[${indices}]" string => join("${const.endl}", "lines[${indices}]");
      "canonified_lines[${indices}]" string => canonify("${lines[${indices}]}");

  files:
      "${tmp}/${files[${file_list}]}"
        create => "true",
        acl    => access_generic("@{initial_acls}");

      "${tmp}/${files[${subfile_list}]}"
        create => "true",
        acl    => access_generic("@{subfile_initial_acls}");
}

#######################################################

bundle agent test
{
  vars:
      "args${init.indices}" slist => { "${init.tmp}/${init.files[${init.indices}]}", "true", "${init.user[${init.indices}]}", "${init.group[${init.indices}]}", "${init.other[${init.indices}]}" };

  methods:
      "ph1" usebundle => apply_gm("permissions_acl_entry", @{args1}, "repaired", "ph1", "enforce" );
      "ph2" usebundle => apply_gm("permissions_acl_entry", @{args2}, "repaired", "ph2", "enforce" );
      "ph3" usebundle => apply_gm("permissions_acl_entry", @{args3}, "repaired", "ph3", "enforce" );
      "ph4" usebundle => apply_gm("permissions_acl_entry", @{args4}, "repaired", "ph4", "enforce" );
      "ph5" usebundle => apply_gm("permissions_acl_entry", @{args5}, "repaired", "ph5", "enforce" );
      "ph6" usebundle => apply_gm("permissions_acl_entry", @{args6}, "repaired", "ph6", "enforce" );
      "ph7" usebundle => apply_gm("permissions_acl_entry", @{args7}, "repaired", "ph7", "enforce" );
      # Do not apply anything on 8-10
      "ph11" usebundle => apply_gm("permissions_acl_entry", @{args11}, "repaired", "ph11", "enforce" );
}

#######################################################

bundle agent check
{
  vars:
    pass1::
      "getfacl_output[${init.indices}]" string => execresult("${paths.getfacl} ${init.tmp}/${init.files[${init.indices}]}", "useshell");

  classes:
      "pass3" expression => "pass2";
      "pass2" expression => "pass1";
      "pass1" expression => "any";

    pass3::
      "lines_matches_${init.create_indices}_${init.canonified_lines[${init.create_indices}]}" not => regcmp(".*${init.lines[${init.create_indices}]}.*","${getfacl_output[${init.create_indices}]}");
      "lines_${init.create_indices}_ok" expression => "lines_matches_${init.create_indices}_${init.canonified_lines[${init.create_indices}]}";
      "lines_not_ok" expression => "lines_${init.create_indices}_ok";


      "classes_ok"  expression => "ph1_ok.ph2_ok.ph3_ok.ph4_ok.ph5_ok.ph6_ok.ph7_ok.ph11_ok";
      "ok"          expression => "classes_ok.!lines_not_ok";

  reports:
    pass3::
      "####################${const.endl}Missing at least one of the folowing lines:${const.endl}${init.printable_lines[${init.indices}]} ${const.endl}in the following output: ${const.endl}${getfacl_output[${init.indices}]}${const.endl}   ####################"
        ifvarclass => "lines_matches_${init.indices}_${init.canonified_lines[${init.indices}]}";
    pass3.ok::
      "$(this.promise_filename) Pass";
    pass3.!ok::
      "$(this.promise_filename) FAIL";
}
