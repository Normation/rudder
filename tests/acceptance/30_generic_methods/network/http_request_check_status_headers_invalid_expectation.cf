#####################################################################################
#
# Test that invalid expectation fails, when querying an existing URL and expecting 404,
#  and that a non existing expecting 200
#
#####################################################################################

bundle common acc_path
{
  vars:
    "root" string => getenv("NCF_TESTS_ACCEPTANCE", 1024);
}

body common control
{
      inputs => { "${acc_path.root}/default.cf.sub", "${acc_path.root}/default_ncf.cf.sub", "@{ncf_inputs.default_files}" };
      bundlesequence  => { configuration, default("$(this.promise_filename)") };
      version => "1.0";
}

#######################################################

bundle agent init
{
  vars:
    "nonexistant_url"       string => "http://www.normation.com/non_existant";
    "existant_url"          string => "http://www.google.fr";
    "canon_nonexistant_url" string => canonify("${nonexistant_url}");
    "canon_existant_url"    string => canonify("${existant_url}");
    "expected_nonexistant"  string => "200";
    "expected_existant"     string => "404";
    "headers"               string => "";
    "method"                string => "GET";

}

#######################################################

bundle agent test
{

  methods:
    "ph" usebundle => http_request_check_status_headers("${init.method}", "${init.nonexistant_url}", "${init.expected_nonexistant}", "${init.headers}");
    "ph" usebundle => http_request_check_status_headers("${init.method}", "${init.existant_url}", "${init.expected_existant}", "${init.headers}");


}

#######################################################

bundle agent check
{
  classes:
      "non_existant_returns_ok" expression => "http_request_check_status_headers_${init.canon_nonexistant_url}_reached.http_request_check_status_headers_${init.canon_nonexistant_url}_not_ok";
      "existant_returns_ok"     expression => "http_request_check_status_headers_${init.canon_existant_url}_reached.http_request_check_status_headers_${init.canon_existant_url}_not_ok";
      "ok"                      expression => "non_existant_returns_ok.existant_returns_ok";

  reports:
    ok::
      "$(this.promise_filename) Pass";
    !ok::
      "$(this.promise_filename) FAIL";

    !non_existant_returns_ok::
      "Wrong status for ${init.nonexistant_url}";

    !existant_returns_ok::
      "Wrong status for ${init.existant_url}";
}
