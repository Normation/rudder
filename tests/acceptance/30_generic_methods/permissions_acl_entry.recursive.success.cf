#######################################################
#
# Test checking if ACLs are present or not
# 
#######################################################

bundle common acc_path
{
  vars:
    "root" string => getenv("NCF_TESTS_ACCEPTANCE", 1024);
}

body common control
{
      inputs => { "${acc_path.root}/default.cf.sub", "${acc_path.root}/default_ncf.cf.sub", "@{ncf_inputs.default_files}" };
      bundlesequence  => { configuration, default("${this.promise_filename}") };
      version => "1.0";
}

#######################################################

bundle agent init
{
  vars:
      "tmp"                        string => getenv("TEMP", 1024);
      "initial_acls_files"         slist => { "user:*:rwx", "group:*:rx", "user:bin:r", "group:bin:r", "all:r", "mask:rx" };
      "initial_acls_directories"   slist => { "user:*:rwx", "group:*:rx", "user:bin:r", "group:bin:r", "all:rx", "mask:rx" };
      "expected_lines_files"       slist => { "user::rwx", "group::r-x", "user:bin:r--", "group:bin:r--", "other::r--", "mask::r-x" };
      "expected_lines_directories" slist => { "user::rwx", "group::r-x", "user:bin:r--", "group:bin:r--", "other::r-x", "mask::r-x" };
      # Cfengine tends to minimize the mask when editing an ACL, do not force it here otherwise it will be recalculated
      # at execution when calling the GM below and we will never have success outcomes.

      "files[1]" string => "file1";
      "user[1]"  string => "*:+rwx";
      "group[1]" string => "";
      "other[1]" string => "r";

      "files[2]" string => "file2";
      "user[2]"  string => "*:rwx";
      "group[2]" string => "*:+w, *:-w, bin:-wx";
      "other[2]" string => "r";

      "files[3]" string => "file3";
      "user[3]"  string => "*:+rx, bin:-wx";
      "group[3]" string => "*:+w, *:-w, bin:-wx";
      "other[3]" string => "=r";

      "files[4]" string => "file4/.";
      "user[4]"  string => "*:+rx, bin:-wx";
      "group[4]" string => "*:+w, *:-w, bin:-wx";
      "other[4]" string => "";

      "files[5]" string => "file5";
      "user[5]"  string => "";
      "group[5]" string => "";
      "other[5]" string => "";

      "files[6]" string => "file6";
      "user[6]"  string => "*:+rw";
      "group[6]" string => "";
      "other[6]" string => "";

      "files[7]" string => "file*";
      "user[7]"  string => "*:+rw";
      "group[7]" string => "";
      "other[7]" string => "";

      "files[8]" string => "file4/1/2/3/4/5/subfile1";
      "user[8]"  string => "*:+rx, bin:-wx";
      "group[8]" string => "*:+w, *:-w, bin:-wx";
      "other[8]" string => "";

      "indices" slist => getindices("files");
      # Do not create 7
      "create_indices" slist => { "1", "2", "3", "4", "5", "6", "8" };
      "lines[${indices}]" slist => { @{expected_lines_files} };
      "lines[4]" slist => { @{expected_lines_directories} };

      "printable_lines[${indices}]" string => join("${const.endl}", "lines[${indices}]");
      "canonified_lines[${indices}]" string => canonify("${lines[${indices}]}");

  files:
      "${tmp}/${files[${create_indices}]}"
        create => "true",
        acl => access_generic("@{initial_acls_files}");

      "${tmp}/${files[4]}"
        create => "true",
        depth_search => recurse_with_base("3"),
        acl => access_generic("@{initial_acls_directories}");
}

#######################################################

bundle agent test
{
  vars:
      "args${init.indices}" slist => { "${init.tmp}/${init.files[${init.indices}]}", "true", "${init.user[${init.indices}]}", "${init.group[${init.indices}]}", "${init.other[${init.indices}]}" };

  methods:
      "ph1" usebundle => apply_gm("permissions_acl_entry", @{args1}, "success", "ph1", "enforce" );
      "ph2" usebundle => apply_gm("permissions_acl_entry", @{args2}, "success", "ph2", "enforce" );
      "ph3" usebundle => apply_gm("permissions_acl_entry", @{args3}, "success", "ph3", "enforce" );
      "ph4" usebundle => apply_gm("permissions_acl_entry", @{args4}, "success", "ph4", "enforce" );
      "ph5" usebundle => apply_gm("permissions_acl_entry", @{args5}, "success", "ph5", "enforce" );
      "ph6" usebundle => apply_gm("permissions_acl_entry", @{args6}, "success", "ph6", "enforce" );
      "ph7" usebundle => apply_gm("permissions_acl_entry", @{args7}, "success", "ph7", "enforce" );
      "ph8" usebundle => apply_gm("permissions_acl_entry", @{args8}, "success", "ph8", "enforce" );
}

#######################################################

bundle agent check
{
  vars:
    pass1::
      "getfacl_output[${init.indices}]" string => execresult("${paths.getfacl} ${init.tmp}/${init.files[${init.indices}]}", "useshell");

  classes:
      "pass3" expression => "pass2";
      "pass2" expression => "pass1";
      "pass1" expression => "any";

    pass3::
      "lines_matches_${init.create_indices}_${init.canonified_lines[${init.create_indices}]}" not => regcmp(".*${init.lines[${init.create_indices}]}.*","${getfacl_output[${init.create_indices}]}");
      "lines_${init.create_indices}_ok" expression => "lines_matches_${init.create_indices}_${init.canonified_lines[${init.create_indices}]}";
      "lines_not_ok" expression => "lines_${init.create_indices}_ok";

      
      "classes_ok"  expression => "ph1_ok.ph2_ok.ph3_ok.ph4_ok.ph5_ok.ph6_ok.ph7_ok.ph8_ok";
      "ok"          expression => "classes_ok.!lines_not_ok";

  reports:
    pass3::
      "####################${const.endl}Missing at least one of the folowing lines:${const.endl}${init.printable_lines[${init.indices}]} ${const.endl}in the following output: ${const.endl}${getfacl_output[${init.indices}]}${const.endl}   ####################"
        ifvarclass => "lines_matches_${init.indices}_${init.canonified_lines[${init.indices}]}";
    pass3.ok::
      "$(this.promise_filename) Pass";
    pass3.!ok::
      "$(this.promise_filename) FAIL";
}
