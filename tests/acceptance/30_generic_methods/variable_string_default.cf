#######################################################
#
# Create a string variable
#
#######################################################

bundle common acc_path
{
  vars:
    "root" string => getenv("NCF_TESTS_ACCEPTANCE", 1024);
}

body common control
{
      inputs => { "${acc_path.root}/default.cf.sub", "${acc_path.root}/default_ncf.cf.sub", "@{ncf_inputs.default_files}" };
      bundlesequence  => { configuration, default("${this.promise_filename}") };
      version => "1.0";
}

#######################################################

bundle agent init
{
  vars:
    "var" string => "var2";
    "prefix" string => "test";
}

#######################################################

bundle agent test
{
  methods:
    "ph1" usebundle => variable_string_default("${init.prefix}", "var1", "init.var", "default1");
    "ph2" usebundle => variable_string_default("test", "var2", "init.var", "default2");
    "ph3" usebundle => variable_string_default("test", "var3", "init.undef", "default3");
}

#######################################################

bundle agent check
{
  classes:
    
    "ok_1" expression => "variable_string_default_var1_kept.!variable_string_default_var1_repaired.!variable_string_default_var1_error";
    "ok_2" expression => "variable_string_default_var2_kept.!variable_string_default_var2_repaired.!variable_string_default_var2_error";
    "ok_3" expression => "variable_string_default_var3_kept.!variable_string_default_var3_repaired.!variable_string_default_var3_error";
    "ok_var1" expression => strcmp("${test.var1}", "${init.var}");
    "ok_var1_bis" expression => strcmp("${${init.prefix}.var1}", "${init.var}");
    "ok_var2"  expression => strcmp("${test.var2}", "${init.var}");
    "ok_var2_bis"  expression => strcmp("${${init.prefix}.${${init.prefix}.var2}}", "${init.var}");
    "ok_var3" expression => strcmp("${test.var3}", "default3");

    "ok"  expression => "ok_1.ok_2.ok_var1.ok_var1_bis.ok_var2.ok_var2_bis.ok_3.ok_var3";

  reports:
    ok::
      "$(this.promise_filename) Pass";
    !ok::
      "$(this.promise_filename) FAIL";
}
