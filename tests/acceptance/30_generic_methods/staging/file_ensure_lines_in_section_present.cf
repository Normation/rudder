#######################################################
#
# Test checking if a line is present in a file
#
#######################################################

bundle common acc_path
{
  vars:
    "root" string => getenv("NCF_TESTS_ACCEPTANCE", 1024);
}

body common control
{
      inputs => { "${acc_path.root}/default.cf.sub", "${acc_path.root}/default_ncf.cf.sub", "@{ncf_inputs.default_files}" };
      bundlesequence  => { default("${this.promise_filename}") };
      version => "1.0";
}

#######################################################

bundle agent init
{
  vars:
    "tmp"             string => getenv("TEMP", 1024);
    "file"            string => "${tmp}/test.ini";
    "file_canon"      string => canonify("${file}");
    "section"         string => "test";

    "options[test][option]" string => "content";

}

#######################################################

bundle agent test
{
  methods:
    "ph1" usebundle => file_ensure_lines_in_section_present("${init.file}", "${init.section}", "init.options");
}

#######################################################

bundle agent check
{
  vars:
    "line_exists_test" string => "/bin/grep -Pzo \"^\\[${init.section}\\]\\\\noption=${init.options[test][option]}$\" \"${init.file}\"";

  classes:
    # By default, file_ensure_lines_in_section_present should create the file if it doesn't exist
    "file_exists" expression => fileexists("${init.file}");
    "line_exists" expression => returnszero("${line_exists_test}", "noshell");

    "ok"          expression => "file_exists.line_exists.file_ensure_lines_in_section_present_${init.file_canon}_ok.!file_ensure_lines_in_section_present_${init.file_canon}_error";

  reports:
    ok::
      "$(this.promise_filename) Pass";
    !ok::
      "$(this.promise_filename) FAIL";
    !file_exists::
      "fileexists returns false for file ${init.file}";
    !line_exists::
      "grep command doesn't return 0 for command: ${line_exists_test}";
}
