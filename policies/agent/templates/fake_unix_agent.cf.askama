body common control
{
      # we're not supposed to make CFEngine calls but just in case
        protocol_version => "2";
        tls_min_version  => "1.3";

        inputs => {
                    @{lib.files},
                    "{{ technique_file_path }}",
                    "{{ directive_file_path }}"
        };

        bundlesequence => {
          "main",
          "export_datastate"
        };
}

body agent control
{
        ifelapsed => "1";
        environment => { "DEBIAN_FRONTEND=noninteractive" };
        abortclasses => { "should_not_continue", "abort_agent_run" };
        default_repository => "{{ work_dir }}/modified-files";
        #agentfacility => "NONE";
}

bundle common lib
{
  vars:
      "ncf_tree"               string => "{{ library_folder }}";
      # Almost same as promises.cf, but only load necessary parts
      "list_compatible_inputs" string => "/bin/sh ${ncf_tree}/10_ncf_internals/list-compatible-inputs";
      "generic_framework"      string => execresult("${list_compatible_inputs} --ncf-path ${ncf_tree} 10_ncf_internals 20_cfe_basics 30_generic_methods", "useshell");
      "default_files_relative"  slist => splitstring("${generic_framework}", "\n", 10000);
      "files"                   slist => maplist("${ncf_tree}/${this}", default_files_relative);
}

bundle common node
{
  vars:
      "properties" data => '{ "rudder": { "packages": { "installed_cache_expire": 60, "updates_cache_expire": 240 } } }';
}

# "g" vars used by ncf
bundle common g
{
  vars:
      # TODO tmpdir?
      "agent_run_interval"         string => "5";
      "rudder_var"                 string => "/var/rudder";
      "rudder_base"                string => "/opt/rudder";
      "rudder_verify_certs_option" string => "";
      "rudder_curl"                string => "/usr/bin/curl",
        unless => fileexists("${rudder_base}/bin/curl");
      "rudder_curl"                string => "${rudder_bin}/curl",
        if => fileexists("${rudder_base}/bin/curl");
      # hardcoded for all tests
      "uuid"                       string => "fb264042-a1b8-4770-b090-a398ea6fbbc3";
      "rudder_node_config_id"      string => "1";
      "execRun"                    string => execresult("/bin/date -u \"+%Y-%m-%d %T+00:00\"", "noshell");

  classes:
      # OS classes for compatibility
      "SUSE" expression => "sles";
      "SuSE" expression => "sles";
      "suse" expression => "sles";
}

body action enforce
{
  action_policy => "fix";
}

bundle agent export_datastate
{
  classes:
    "pass3" expression => "pass2";
    "pass2" expression => "pass1";
    "pass1" expression => "any";
  files:
    pass3::
      "{{ datastate_file_path }}"
        template_method      => "inline_mustache",
        create               => "true",
        action               => enforce,
        edit_template_string => {% raw %}"{{%-top-}}";{% endraw %}
}
