#######################################################
#
# Test checking if a line can be replaced in a file
#
#######################################################

bundle common acc_path
{
  vars:
    "root" string => getenv("NCF_TESTS_ACCEPTANCE", 1024);
}

body common control
{
      inputs => { "${acc_path.root}/default.cf.sub", "${acc_path.root}/default_ncf.cf.sub", "@{ncf_inputs.default_files}" };
      bundlesequence  => { configuration, default("${this.promise_filename}") };
      version => "1.0";
}

#######################################################

bundle agent init
{
  vars:
    "tmp"                  string => getenv("TEMP", 1024);
    "file"                 string => "${tmp}/test";
    "file_canon"           string => canonify("${file}");
    "line_to_add"          string => "This is a test line!";
    "line_for_replacement" string => "This is a better test line!";

  methods:
    "ph1" usebundle => file_ensure_lines_present("${file}", "${line_to_add}");
}

#######################################################

bundle agent test
{
  methods:
    "ph1" usebundle => file_replace_lines("${init.file}", "${init.line_to_add}", "${init.line_for_replacement}");
}

#######################################################

bundle agent check
{
  vars:
    "line_exists_test" string => "${paths.path[grep]} -E \"^${init.line_for_replacement}$\" \"${init.file}\"";

  classes:
    # By default, file_replace_lines should create the file if it doesn't exist
    "file_exists" expression => fileexists("${init.file}");
    "line_exists"
      expression => returnszero("${line_exists_test}", "noshell"),
      ifvarclass => canonify("file_replace_lines_${init.file}_reached");

    "ok"          expression => "file_exists.line_exists.file_replace_lines_${init.file_canon}_ok.!file_replace_lines_${init.file_canon}_error";

  reports:
    ok::
      "$(this.promise_filename) Pass";
    !ok::
      "$(this.promise_filename) FAIL";
    !file_exists::
      "fileexists returns false for file ${init.file}";
    !line_exists::
      "grep command doesn't return 0 for command: ${line_exists_test}";
}
