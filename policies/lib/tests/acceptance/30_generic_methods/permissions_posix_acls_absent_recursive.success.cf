#######################################################
#
# Test checking if ACLs are present or not
#
# Create: acl_absent_test/sub_dir1/file1
#         acl_absent_test/file1
#
# Remove ACLs on acl_absent_test/sub_dir1/.
#             on acl_absent_test/sub_dir1/file1
#            and acl_absent_test/file1
#
# Apply permissions_posix_acls_absent in enforce mode without recursion on
#         acl_absent_test/sub_dir1
#         acl_absent_test/file1
#         acl_absent_test/*
# They should all succeed since they have no ACLs set
#
#######################################################

bundle common acc_path
{
  vars:
    "root" string => getenv("NCF_TESTS_ACCEPTANCE", 1024);
}

body common control
{
      inputs => { "${acc_path.root}/default.cf.sub", "${acc_path.root}/default_ncf.cf.sub", "@{ncf_inputs.default_files}" };
      bundlesequence  => { configuration, default("${this.promise_filename}") };
      version => "1.0";
}

#######################################################

bundle agent init
{
  vars:
      "tmp"   string => getenv("TEMP", 1024);

      "files[1]" string => "acl_absent_test/sub_dir1/.";
      "files[2]" string => "acl_absent_test/sub_dir1/file1";
      "files[3]" string => "acl_absent_test/file1";
      "files[4]" string => "acl_absent_test/*";

      "indices" slist => { "1", "2", "3", "4" };
      "create_indices" slist => { "1", "2", "3" };
      "effective_indices" slist => { "1", "3", "4" };

      "files_canon[${indices}]" string => canonify("${files[${indices}]}");

      "expected_class_prefix[${effective_indices}]" string => canonify("permissions_posix_acls_absent_${tmp}/${files[${effective_indices}]}_true");
      "expected_old_class_prefix[${effective_indices}]" string => canonify("permissions_posix_acls_absent_${tmp}/${files[${effective_indices}]}");

  classes:
      "pass3" expression => "pass2";
      "pass2" expression => "pass1";
      "pass1" expression => "any";

    pass2::
      "acls_set_${files_canon[${create_indices}]}" expression => returnszero("${paths.setfacl} -b ${tmp}/${files[${create_indices}]}", "useshell"),
        scope => "namespace";

  files:
    any::
      "${tmp}/${files[${create_indices}]}"
        create => "true",
        perms  => mog("555", "root", "0");

  methods:
    "any" usebundle => define_expected_classes("${expected_old_class_prefix[${effective_indices}]}", "success", "old_${effective_indices}");
    "any" usebundle => define_expected_classes("${expected_class_prefix[${effective_indices}]}", "success", "${effective_indices}");
}

#######################################################

bundle agent test
{
  methods:
    "ph${init.effective_indices}" usebundle => permissions_posix_acls_absent("${init.tmp}/${init.files[${init.effective_indices}]}", "true");
}

#######################################################

bundle agent check
{
  vars:
    "getfacl_output[${init.create_indices}]" string => execresult("${paths.getfacl} --skip-base ${init.tmp}/${init.files[${init.create_indices}]}", "useshell"),
      ifvarclass => "acls_set_${init.files_canon[${init.create_indices}]}";

  classes:
   "old_class_${init.effective_indices}" expression => "${define_expected_classes.report_string_old_${init.effective_indices}}";
    "class_${init.effective_indices}" expression => "${define_expected_classes.report_string_${init.effective_indices}}";

   "old_class_prefix_ok" expression => "old_class_1.old_class_3.old_class_3";
   "class_prefix_ok" expression => "class_1.class_3.class_4";

   "cleanup_${init.create_indices}" expression => strcmp("${getfacl_output[${init.indices}]}", "");
   "all_cleanup" expression => "cleanup_1.cleanup_2.cleanup_3";

   "ok" expression => "all_cleanup.class_prefix_ok.old_class_prefix_ok";

  reports:
    ok::
      "$(this.promise_filename) Pass";
    !ok::
      "$(this.promise_filename) FAIL";
    any::
      "ACLs change found on ${init.tmp}/${init.files[${init.crceate_indices}]}"
      ifvarclass => "!cleanup_${init.create_indices}";

      "Missing expected old class for ${init.files[${init.effective_indices}]}"
      ifvarclass => "!old_class_${init.effective_indices}";

      "Missing expected class for ${init.files[${init.effective_indices}]}"
      ifvarclass => "!class_${init.effective_indices}";
}
