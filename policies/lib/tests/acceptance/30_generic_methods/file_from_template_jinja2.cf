####################################################################################
# Copyright 2016 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# agent_version >= 3.9

#######################################################
#
# Test checking if a file can be generated from a local template
#
#######################################################

bundle common acc_path
{
  vars:
    "root" string => getenv("NCF_TESTS_ACCEPTANCE", 1024);
}

body common control
{
      inputs => { "${acc_path.root}/default.cf.sub", "${acc_path.root}/default_ncf.cf.sub", "@{ncf_inputs.default_files}" };
      bundlesequence  => { configuration, initialization, default("${this.promise_filename}") };
      version => "1.0";
}

#######################################################

bundle agent init
{
  vars:
    "tmp"                    string => getenv("TEMP", 1024);
    "source_template_1"      string => "${this.promise_filename}_1.j2";
    "source_template_2"      string => "${this.promise_filename}_2.j2";
    "expected_file_1"        string => "${this.promise_filename}_1.expected";
    "expected_file_2"        string => "${this.promise_filename}_2.expected";

    "destination_file_1"     string => "${tmp}/destination_test_1";
    "destination_file_2"     string => "${tmp}/destination_test_2";
    "destination_file_canon_1" string => canonify("${destination_file_1}");
    "destination_file_canon_2" string => canonify("${destination_file_2}");

    "test_variable"          string => "test_value";
    "my_list"                slist => { "test1", "test2" };
    
  classes:
    "my_global_class" expression => "any", scope => "namespace";

}

#######################################################

bundle agent test
{
  methods:
    "ph1" usebundle => file_from_template_jinja2("${init.source_template_1}", "${init.destination_file_1}");
    "ph2" usebundle => file_from_template_jinja2("${init.source_template_2}", "${init.destination_file_2}");
}

#######################################################

bundle agent check
{
                    
  vars:
    "file_diff_test_1" string => "/usr/bin/diff \"${init.destination_file_1}\" \"${init.expected_file_1}\"";
    "file_diff_test_2" string => "/usr/bin/diff \"${init.destination_file_2}\" \"${init.expected_file_2}\"";

  classes:
    "file_ok_1" expression => returnszero("${file_diff_test_1}", "noshell"),
                ifvarclass => "file_from_template_${init.destination_file_canon_1}_reached";
    "file_ok_2" expression => returnszero("${file_diff_test_2}", "noshell"),
                ifvarclass => "file_from_template_${init.destination_file_canon_2}_reached";

    "ok_1"      expression => "file_ok_1.file_from_template_${init.destination_file_canon_1}_ok.!file_from_template_${init.destination_file_canon_1}_error";
    "ok_2"      expression => "file_ok_2.file_from_template_${init.destination_file_canon_2}_ok.!file_from_template_${init.destination_file_canon_2}_error";
    "ok"        expression => "ok_1.ok_2";

  reports:
    ok::
      "$(this.promise_filename) Pass";
    !ok_1::
      "$(this.promise_filename) FAIL: diff command doesn't returns 0 (command: ${file_diff_test_1} )";
    !ok_2::
      "$(this.promise_filename) FAIL: diff command doesn't returns 0 (command: ${file_diff_test_2} )";

}
