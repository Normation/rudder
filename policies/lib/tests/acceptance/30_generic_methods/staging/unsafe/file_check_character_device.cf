#########################################
#
# Test checking if a character device or not 
# 
#########################################

bundle common acc_path
{
  vars:
    "root" string => getenv("NCF_TESTS_ACCEPTANCE", 1024);
}

body common control
{
      inputs => { "${acc_path.root}/default.cf.sub", "${acc_path.root}/default_ncf.cf.sub", "@{ncf_inputs.default_files}" };
      bundlesequence  => { configuration, default("${this.promise_filename}") };
      version => "1.0";
}

#######################################################

bundle agent init
{
  vars:
    "tmp"            string => getenv("TEMP", 1024);
    "file"           string => "${tmp}/test";
    "file_canon"     string => canonify("${file}");

    "character_device"        string => "${tmp}/character_device";
    "character_device_canon"  string => canonify("${character_device}");

    "file_list"       slist => { "${file}", "${character_device}" };

  files:
    "${file}"
      create => "true";

  commands:
    "/bin/mknod"
      args => "-m 640 \"${character_device}\" c 5 1"; # /dev/console
}

#######################################################

bundle agent test
{
  methods:
    "fce" usebundle => file_check_character_device("${init.file_list}");
}

#######################################################

bundle agent check
{
  vars:
    # Check file type
    "test_file_type"        string => "/usr/bin/file -b \"${init.file}\"";

    # Check character_device type
    "test_character_device_type"     string => "/usr/bin/file -b \"${init.character_device}\"";

  classes:
    "test_file_is_character_device"
      expression => regcmp("character special.*", execresult("${test_file_type}", "noshell") ),
      ifvarclass => "file_check_character_device_${init.file_canon}_reached";

    "test_character_device_is_character_device"
      expression => regcmp("character special.*", execresult("${test_character_device_type}", "noshell") ),
      ifvarclass => "file_check_character_device_${init.character_device_canon}_reached";

    "ok"   expression => "test_character_device_is_character_device.file_check_character_device_${init.character_device_canon}_ok.!test_file_is_character_device.!file_check_character_device_${init.file_canon}_ok.file_check_character_device_${init.file_canon}_reached";

  reports:
    ok::
      "$(this.promise_filename) Pass";
    !ok::
      "$(this.promise_filename) FAIL";
    !test_character_device_is_character_device::
      "${init.character_device} is not a character_device as expected. It is '${test_character_device_type}'";
    test_file_is_character_device::
      "${init.file} is a character_device, not as expected. It is '${test_file_type}'";

    cfengine::
      "Check of ${init.file} is not reached"
        ifvarclass => "!file_check_character_device_${init.file_canon}_reached";
      "Check of ${init.character_device} is not reached"
        ifvarclass => "!file_check_character_device_${init.character_device_canon}_reached";

}
