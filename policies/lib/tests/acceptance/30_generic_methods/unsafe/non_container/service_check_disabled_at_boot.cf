#######################################################
#
# Test if disable cron at boot, and try with an unknown service to check error case
#
#######################################################

bundle common acc_path
{
  vars:
    "root" string => getenv("NCF_TESTS_ACCEPTANCE", 1024);
}

body common control
{
      inputs => { "${acc_path.root}/default.cf.sub", "${acc_path.root}/default_ncf.cf.sub", "@{ncf_inputs.default_files}" };
      bundlesequence  => { configuration, default("${this.promise_filename}") };
      version => "1.0";
}

#######################################################

bundle agent init
{
  vars:
    ubuntu::
      "service_name"  string => "ntp";
    (debian|suse).!ubuntu::
      "service_name"  string => "cron";
    redhat::
      "service_name"  string => "crond";
    any::
      "unknown_service_name"  string => "unknown";

  classes:
    debian::
      "enabled" expression => returnszero("update-rc.d ${service_name} disable", "useshell");
    redhat|suse::
      "enabled" expression => returnszero("chkconfig ${service_name} off", "useshell");
}

#######################################################

bundle agent test
{
  methods:
    "ph1" usebundle => service_check_disabled_at_boot("${init.service_name}");
    "ph3" usebundle => service_check_disabled_at_boot("${init.unknown_service_name}");
}

#######################################################

bundle agent check
{
  classes:
    "ok" expression => "service_check_disabled_at_boot_${init.service_name}_ok.!service_check_disabled_at_boot_${init.service_name}_error.service_check_disabled_at_boot_${init.unknown_service_name}_ok";

  reports:
    ok::
      "$(this.promise_filename) Pass";
    !ok::
      "$(this.promise_filename) FAIL";
}
