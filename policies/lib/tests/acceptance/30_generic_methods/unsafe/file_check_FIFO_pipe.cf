#########################################
#
# Test checking if a file is a FIFO or not 
# 
#########################################

bundle common acc_path
{
  vars:
    "root" string => getenv("NCF_TESTS_ACCEPTANCE", 1024);
}

body common control
{
      inputs => { "${acc_path.root}/default.cf.sub", "${acc_path.root}/default_ncf.cf.sub", "@{ncf_inputs.default_files}" };
      bundlesequence  => { configuration, default("${this.promise_filename}") };
      version => "1.0";
}

#######################################################

bundle agent init
{
  vars:
    "tmp"            string => getenv("TEMP", 1024);
    "file"           string => "${tmp}/test";
    "file_canon"     string => canonify("${file}");

    "FIFO_pipe"        string => "${tmp}/FIFO_pipe";
    "FIFO_pipe_canon"  string => canonify("${FIFO_pipe}");

    "file_list"       slist => { "${file}", "${FIFO_pipe}" };

  files:
    "${file}"
      create => "true";

  commands:
    "/usr/bin/mkfifo"
      args => "-m 640 \"${FIFO_pipe}\"";
}

#######################################################

bundle agent test
{
  methods:
    "fce" usebundle => file_check_FIFO_pipe("${init.file_list}");
}

#######################################################

bundle agent check
{
  vars:
    # Check file type
    "test_file_type"          string => "/usr/bin/file -b \"${init.file}\"";

    # Check FIFO_pipe type
    "test_FIFO_pipe_type"     string => "/usr/bin/file -b \"${init.FIFO_pipe}\"";

  classes:
    "test_file_is_FIFO_pipe"
      expression => strcmp("fifo (named pipe)", execresult("${test_file_type}", "noshell") ),
      ifvarclass => "file_check_FIFO_pipe_${init.file_canon}_reached";

    "test_FIFO_pipe_is_FIFO_pipe"
      expression => strcmp("fifo (named pipe)", execresult("${test_FIFO_pipe_type}", "noshell") ),
      ifvarclass => "file_check_FIFO_pipe_${init.FIFO_pipe_canon}_reached";

    "ok"   expression => "test_FIFO_pipe_is_FIFO_pipe.file_check_FIFO_pipe_${init.FIFO_pipe_canon}_ok.!test_file_is_FIFO_pipe.!file_check_FIFO_pipe_${init.file_canon}_ok.file_check_FIFO_pipe_${init.file_canon}_reached";

  reports:
    ok::
      "$(this.promise_filename) Pass";
    !ok::
      "$(this.promise_filename) FAIL";
    !test_FIFO_pipe_is_FIFO_pipe::
      "${init.FIFO_pipe} is not a FIFO_pipe as expected. It is '${test_FIFO_pipe_type}'";
    test_file_is_FIFO_pipe::
      "${init.file} is a FIFO_pipe, not as expected. It is '${test_file_type}'";

    cfengine::
      "Check of ${init.file} is not reached"
        ifvarclass => "!file_check_FIFO_pipe_${init.file_canon}_reached";
      "Check of ${init.FIFO_pipe} is not reached"
        ifvarclass => "!file_check_FIFO_pipe_${init.FIFO_pipe_canon}_reached";

}
