#######################################################
#
# Test ncf dry-run using the global_dry_run class
#
#######################################################

# @agent_requirements "capabilities": [ "bodydefault" ] || "agent_version" >= 3.9

bundle common acc_path
{
  vars:
    "root" string => getenv("NCF_TESTS_ACCEPTANCE", 1024);
}

body common control
{
      inputs => { "${acc_path.root}/default.cf.sub", "${acc_path.root}/default_ncf.cf.sub", "@{ncf_inputs.default_files}" };
      bundlesequence  => { configuration, default("${this.promise_filename}") };
      version => "1.0";
}

#######################################################

bundle agent init {}

#######################################################

bundle agent test_action(name)
{
  files:
	  "$(G.testdir)/${name}"
             create => "true",
             classes => classes_generic("create_${name}");
}

#######################################################

bundle agent test
{

  classes:
      "global_dry_run" expression => "create_default_reached", scope => "namespace";

  methods:
      "default"
        usebundle => test_action("default");
    global_dry_run::
      "dryrun"
        usebundle => test_action("dryrun");
}

bundle agent check
{
  classes:
    "default_created" expression => fileexists("$(G.testdir)/default");
    "dryrun_created" expression => fileexists("$(G.testdir)/dryrun");
    "ok" expression => "create_dryrun_reached.!dryrun_created.default_created";

  reports:   
    ok::
      "$(this.promise_filename) Pass";
    !ok::
      "$(this.promise_filename) FAIL";
}

