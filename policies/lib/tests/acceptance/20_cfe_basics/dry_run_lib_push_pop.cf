#######################################################
#
# Test ncf dry-run using the push/pop_dry_run_mode call
#
#######################################################

# @agent_requirements "capabilities": [ "bodydefault" ] || "agent_version" >= 3.9

bundle common acc_path
{
  vars:
    "root" string => getenv("NCF_TESTS_ACCEPTANCE", 1024);
}

body common control
{
      inputs => { "${acc_path.root}/default.cf.sub", "${acc_path.root}/default_ncf.cf.sub", "@{ncf_inputs.default_files}" };
      bundlesequence  => { configuration, default("${this.promise_filename}") };
      version => "1.0";
}

#######################################################

bundle agent init {}

#######################################################

bundle agent test_action(name)
{
  files:
	  "$(G.testdir)/${name}"
             create => "true",
             classes => classes_generic("create_${name}");
}

#######################################################

bundle agent test
{

#   "enable dry-run"  usebundle => push_dry_run_mode("true");
#   "dry-run part"    usebundle => package_present("htop");
#   "restore dry-run" usebundle => pop_dry_run_mode();


  methods:
      "default"
        usebundle => test_action("default");
      "enable dry-run"
        usebundle => push_dry_run_mode("true");
      "dryrun"
        usebundle => test_action("dryrun");
      "disable dry-run"
        usebundle => push_dry_run_mode("false");
      "not dryrun"
        usebundle => test_action("notdryrun");
      "pop dry-run"
        usebundle => pop_dry_run_mode;
      "dryrun2"
        usebundle => test_action("dryrun2");
      "pop dry-run2"
        usebundle => pop_dry_run_mode; 
      "not dryrun2"
        usebundle => test_action("notdryrun2");

}

bundle agent check
{
  classes:
    "default_created" expression => fileexists("$(G.testdir)/default");
    "dryrun_created" expression => fileexists("$(G.testdir)/dryrun");
    "dryrun2_created" expression => fileexists("$(G.testdir)/dryrun2");
    "notdryrun_created" expression => fileexists("$(G.testdir)/notdryrun");
    "notdryrun2_created" expression => fileexists("$(G.testdir)/notdryrun2");
    "ok" expression => "create_dryrun_reached.!dryrun_created.default_created.notdryrun_created.!dryrun2_created.notdryrun2_created";

  reports:   
    ok::
      "$(this.promise_filename) Pass";
    !ok::
      "$(this.promise_filename) FAIL";
}

