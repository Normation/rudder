#####################################################################################
# Copyright 2017 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name User shell
# @description Define the shell of the user. User must already exist.
# @documentation This method does not create the user.
#   entry example: /bin/false
#
# @parameter login User's login
# @parameter shell User's shell
# 
# @class_prefix user_shell
# @class_parameter login
# @agent_support = ["cfengine-community"]

bundle agent user_shell(login, shell)
{
  vars:
      "class_prefix"          string => canonify("user_shell_${login}");
      "result_classes_prefix"  slist => {"${class_prefix}", "${report_data.method_id}"};

  classes:
      "user_exists" expression => userexists("${login}");
      
      "pass3" expression => "pass2";
      "pass2" expression => "pass1";
      "pass1" expression => "any";

  users:
      "${login}"
         policy => "present",
          shell => "${shell}",
        classes => classes_generic_two("${report_data.method_id}", "${class_prefix}"),
             if => "user_exists";

  methods:
    pass3.!user_exists::
      "${report_data.method_id}" usebundle => _classes_failure("${result_classes_prefix}");
      "${report_data.method_id}" usebundle => log_rudder_v4("${login}", "User ${login} does not exist. Can't set user ${login} shell to ${shell}", "");
    
    pass3.user_exists::
      "${report_data.method_id}" usebundle => log_rudder_v4("${login}", "User ${login} shell set to ${shell}", "");
}

