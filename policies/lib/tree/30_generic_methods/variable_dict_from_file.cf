#####################################################################################
# Copyright 2015 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name Variable dict from JSON file
# @description Define a variable that contains key,value pairs (a dictionary) from a JSON file
# @documentation To use the generated variable, you must use the form `${prefix.name[key]}` with each name replaced with the parameters of this method.
# 
# Be careful that using a global variable can lead to unpredictable content in case of multiple definition, which is implicitly the case when a technique has more than one instance (directive). 
# Please note that only global variables are available within templates.
#
# See [variable_dict_from_file_type](#_variable_dict_from_file_type) for complete documentation.
# @parameter prefix The prefix of the variable name
# @parameter_constraint prefix "regex" : "^(_|[^\p{Punct}\s])+$"
# @parameter name   The variable to define, the full name will be prefix.name
# @parameter_constraint name "regex" : "^(_|[^\p{Punct}\s])+$"
# @parameter file_name       The absolute local file name with JSON content
#
# 
# @parameter_rename variable_prefix prefix
# @parameter_rename variable_name name
# @class_prefix variable_dict_from_file
# @class_parameter name
# @agent_support = ["cfengine-community", "dsc"]

bundle agent variable_dict_from_file(prefix, name, file_name)
{
  vars:
      "old_class_prefix"  string => canonify("variable_dict_from_file_${name}");
      "args"               slist => { "${prefix}", "${name}", "${file_name}" };
      "report_param"      string => join("_", args);
      "full_class_prefix" string => canonify("variable_dict_from_file_${report_param}");
      "class_prefix"      string => string_head("${full_class_prefix}", "1000");

      # define the variable within the prefix namespace
      "${prefix}.${name}"  data => readjson("${file_name}", "100000000");

  classes:
     "variable_defined" expression => isvariable("${prefix}.${name}");

  methods:
    !variable_defined::
      "error"    usebundle => _classes_failure("${old_class_prefix}");
      "error"    usebundle => _classes_failure("${class_prefix}");

    variable_defined::
      "success"  usebundle => _classes_success("${old_class_prefix}");
      "success"  usebundle => _classes_success("${class_prefix}");

    any::
      "report"
        usebundle  => _log_v3("Set the dict ${prefix}.${name} to the content of ${file_name}", "${name}", "${old_class_prefix}", "${class_prefix}", @{args});
}
