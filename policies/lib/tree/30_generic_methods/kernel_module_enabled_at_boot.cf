#######################################################################################
# Copyright 2019 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name Kernel module enabled at boot
# @description Ensure that a given kernel module will be loaded at system boot
# @documentation Ensure that a given kernel module is enabled at boot on the system.
#  This method only works on systemd systems.
#  Rudder will look for a line matching the module name in a given section in the file:
#
#  * `/etc/modules-load.d/enabled_by_rudder.conf` on systemd systems
#
#  If the module is already enabled by a different option file than used by Rudder, it will add
#  an entry in the file managed by Rudder listed above, and leave intact the already present one.
#  The modifications are persistent and made line per line, meaning that
#  this Generic Method will never remove lines in the configuration file but only add it if needed.
#
#  Please note that this method will not load the module nor configure it, it will only enable its loading at system boot.
#  If you want to force the module to be loaded, use instead the method `kernel_module_loaded`.
#  If you want to configure the module, use instead the method `kernel_module_configuration`.
#
# @parameter name Complete name of the kernel module, as seen by lsmod or listed in /proc/modules
#
#
# @parameter_rename module_name name
# @class_prefix kernel_module_enabled_at_boot
# @class_parameter name
# @agent_support = ["cfengine-community"]


bundle agent kernel_module_enabled_at_boot(name)
{
  vars:
    systemd::
      "module_dir"              string => "/etc/modules-load.d";
    any::
      "class_prefix"        string => canonify("kernel_module_enabled_at_boot_${name}");

  classes:
      "pass3" expression => "pass2";
      "pass2" expression => "pass1";
      "pass1" expression => "any";

  methods:
    pass3.systemd::
      "${report_data.method_id}" usebundle => call_method("file_ensure_lines_present");
      "${report_data.method_id}" usebundle => file_ensure_lines_present("${module_dir}/enabled_by_rudder.conf", "${name}");
      "${report_data.method_id}" usebundle => call_method_classes("${class_prefix}");
      "${report_data.method_id}" usebundle => call_method_classes_caller;
      "${report_data.method_id}" usebundle => call_method_end("file_ensure_lines_present");
      "${report_data.method_id}" usebundle => log_rudder_v4("${name}", "Ensure that kernel module ${name} is enabled on the system", "");

    pass3.!systemd::
      "${report_data.method_id}" usebundle => _classes_failure("${class_prefix}");
      "${report_data.method_id}" usebundle => _classes_failure("${report_data.method_id}");
      "${report_data.method_id}" usebundle => log_rudder_v4("${name}", "This method is not supported on non-systemd systems", "");
}
