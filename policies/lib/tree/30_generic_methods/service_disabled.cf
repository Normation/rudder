#####################################################################################
# Copyright 2017 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name Service disabled at boot
# @description Force a service not to be enabled at boot
#
# @parameter name Service name (as recognized by systemd, init.d, etc...)
#
#
# @parameter_rename service_name name
# @class_prefix service_disabled
# @class_parameter name
# @agent_support = ["cfengine-community"]

bundle agent service_disabled(name)
{
  vars:
      "class_prefix" string => "service_disabled_${name}";

  classes:
      "needs_disable" expression => "${report_data.method_id}_ncf_services_check_kept";

      "pass3" expression => "pass2";
      "pass2" expression => "pass1";
      "pass1" expression => "any";

  methods:
    pass2.!pass3::
      # Get service status
      "${report_data.method_id}" usebundle => call_method("ncf_services_check");
      "${report_data.method_id}" usebundle => ncf_services("${name}", "is-enabled");
      "${report_data.method_id}" usebundle => call_method_end("ncf_services_check");
    pass3.needs_disable::
      # If service is running, disable it
      "${report_data.method_id}" usebundle => call_method("ncf_services_disable");
      "${report_data.method_id}" usebundle => ncf_services("${name}", "disable");
      # Use stop result as method result
      "${report_data.method_id}" usebundle => call_method_classes("${class_prefix}");
      "${report_data.method_id}" usebundle => _classes_success("${report_data.report_id}");
      "${report_data.method_id}" usebundle => call_method_classes_caller;
      "${report_data.method_id}" usebundle => call_method_end("ncf_services_disable");
    pass3.!needs_disable::
      # Define success classes is check_running was not kept
      "${report_data.method_id}" usebundle => _classes_success("${class_prefix}");
      "${report_data.method_id}" usebundle => _classes_success("${report_data.method_id}");
      "${report_data.method_id}" usebundle => _classes_success("${report_data.report_id}");
    pass3::
      # Report
      "${report_data.method_id}" usebundle => log_rudder_v4("${name}", "Ensure service ${name} is disabled at boot", "");
}
