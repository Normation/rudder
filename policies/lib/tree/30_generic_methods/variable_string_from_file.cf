#####################################################################################
# Copyright 2015 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name Variable string from file
# @description Define a variable from a file content
# @documentation To use the generated variable, you must use the form `${variable_prefix.variable_name}` with each name replaced with the parameters of this method.
#
# Be careful that using a global variable can lead to unpredictable content in case of multiple definition, which is implicitly the case when a technique has more than one instance (directive). 
# Please note that only global variables are available within templates.
#
# @parameter prefix The prefix of the variable name
# @parameter_constraint prefix "regex" : "^[a-zA-Z_]+$"
# @parameter name   The variable to define, the full name will be variable_prefix.variable_name
# @parameter_constraint name "regex" : "^[a-zA-Z_]+$"
# @parameter file_name       The path of the file
# 
# @parameter_rename variable_prefix prefix
# @parameter_rename variable_name name
# @class_prefix variable_string_from_file
# @class_parameter name
# @agent_support = ["cfengine-community", "dsc"]

bundle agent variable_string_from_file(prefix, name, file_name)
{
  vars:
      "class_prefix"      string => canonify("variable_string_from_file_${name}");
      # define the variable within the variable_prefix namespace
      "${prefix}.${name}" string => readfile("${file_name}" , "0");

  classes:
      "pass3"            expression => "pass2";
      "pass2"            expression => "pass1";
      "pass1"            expression => "any";
      "variable_defined" expression => isvariable("${prefix}.${name}");

  methods:
    pass3.!variable_defined::
      "${report_data.method_id}" usebundle => _classes_failure("${report_data.method_id}");
      "${report_data.method_id}" usebundle => _classes_failure("${class_prefix}");

    pass3.variable_defined::
      "${report_data.method_id}" usebundle => _classes_success("${report_data.method_id}");
      "${report_data.method_id}" usebundle => _classes_success("${class_prefix}");

    pass3::
      "${report_data.method_id}" usebundle => log_rudder_v4("${name}", "Set the string ${prefix}.${name} to the content of ${file_name}", "");
}
