#####################################################################################
# Copyright 2019 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
######################################################################################

# @name Kernel module loaded
# @description Ensure that a given kernel module is loaded on the system
# @documentation Ensure that a given kernel module is loaded on the system.
#   If the module is not loaded, it will try to load it via modprobe.
#
# @parameter name Complete name of the kernel module, as seen by lsmod or listed in /proc/modules
#
#
# @parameter_rename module_name name
# @class_prefix kernel_module_loaded
# @class_parameter name
# @agent_support = ["cfengine-community"]

bundle agent kernel_module_loaded(name)
{
  vars:
      "class_prefix" string => canonify("kernel_module_loaded_${name}");
      "c_name"       string => canonify("${name}");

  classes:
      "pass3" expression => "pass2";
      "pass2" expression => "pass1";
      "pass1" expression => "any";

    pass1.!pass2::
      "${c_name}_was_loaded"  expression => returnszero("/sbin/lsmod | cut -d ' '  -f 1 | ${paths.path[grep]} -q ^${name}$ #Initial check for loading ${name}", "useshell");
      "${c_name}_unload"      expression => returnszero("/sbin/modprobe ${name}", "useshell"),
        if => and( "!${c_name}_was_loaded",
                   "!(global_dry_run|dry_run)"
                 );
    pass3::
      "${c_name}_is_loaded" expression => returnszero("/sbin/lsmod | cut -d ' '  -f 1 | ${paths.path[grep]} -q ^${name}$ #Final check for loading ${name}", "useshell");

  methods:
    "pass3.!${c_name}_is_loaded"::
      "${report_data.method_id}" usebundle => _classes_failure("${report_data.method_id}");
      "${report_data.method_id}" usebundle => _classes_failure("${class_prefix}");

    "pass3.!${c_name}_was_loaded.${c_name}_is_loaded"::
      "${report_data.method_id}" usebundle => _classes_repaired("${report_data.method_id}");
      "${report_data.method_id}" usebundle => _classes_repaired("${class_prefix}");

    "pass3.${c_name}_was_loaded"::
      "${report_data.method_id}" usebundle => _classes_success("${report_data.method_id}");
      "${report_data.method_id}" usebundle => _classes_success("${class_prefix}");

    pass3::
      "${report_data.method_id}" usebundle => log_rudder_v4("${name}", "Check that the kernel module ${name} is loaded on the system", "");
}
