#####################################################################################
# Copyright 2013 Normation SAS
# Copyright 2016 Janos Mattyasovszky
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name File check is hardlink
# @description Checks if two files are the same (hard links)
# @documentation This bundle will define a condition `file_check_hardlink_${path}_{ok, reached, kept}` if the
# two files `${path}` and `${path_2}` are hard links of each other, or `file_check_hardlink_${path}_{not_ok, reached, not_kept, failed}` if
# if the files are not hard links.
#
# @parameter path   File name #1 (absolute path on the target node)
# @parameter path_2   File name #2 (absolute path on the target node)
#
#
# @parameter_rename file_name_1 path
# @parameter_rename file_name_2 path_2
# @class_prefix file_check_hardlink
# @class_parameter path
# @agent_support = ["cfengine-community"]

bundle agent file_check_hardlink(path, path_2)
{
  vars:
      "class_prefix"  string => canonify("file_check_hardlink_${path}");

      "file_1_devno"  string => filestat("${path}", "devno");
      "file_1_ino"    string => filestat("${path}", "ino");
      "file_2_devno"  string => filestat("${path_2}", "devno");
      "file_2_ino"    string => filestat("${path_2}", "ino");


  classes:
      "pass3" expression => "pass2";
      "pass2" expression => "pass1";
      "pass1" expression => "any";
      "file_1_exists"       expression => fileexists("${path}");
      "file_2_exists"       expression => fileexists("${path_2}");
      "files_match_device"  expression => strcmp("${file_1_devno}", "${file_2_devno}");
      "files_match_inode"   expression => strcmp("${file_1_ino}",   "${file_2_ino}");
      "files_are_hardlinks" expression => and("file_1_exists", "file_2_exists", "files_match_device", "files_match_inode");


  methods:
    pass3.files_are_hardlinks::
      "${report_data.method_id}" usebundle => _classes_success("${report_data.method_id}");
      "${report_data.method_id}" usebundle => _classes_success("${class_prefix}");

    pass3.!files_are_hardlinks::
      "${report_data.method_id}" usebundle => _classes_failure("${report_data.method_id}");
      "${report_data.method_id}" usebundle => _classes_failure("${class_prefix}");

    pass3::
      "${report_data.method_id}" usebundle => log_rudder_v4("${path}", "Check if ${path} and ${path_2} are the same", "");
}
