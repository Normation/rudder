#####################################################################################
# Copyright 2013 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name File lines absent
# @description Ensure that a line is absent in a specific location
# @documentation Edit the file, and ensure it does not contain the defined line. Regular expression can be used for both the file name and the lines absent.
#
# @parameter path File name to edit (absolute path on the target node)
# @parameter lines Line(s) to remove in the file
#
#
# @parameter_rename file path
# @class_prefix file_lines_absent
# @class_parameter path
# @agent_support = ["cfengine-community", "dsc"]

bundle agent file_lines_absent(path, lines)
{
  vars:
      "class_prefix"       string => canonify("file_lines_absent_${path}");
      "inner_class_prefix" string => "${report_data.method_id}_inner";

  classes:
      "pass3"       expression => "pass2";
      "pass2"       expression => "pass1";
      "pass1"       expression => "any";
      "file_exists" expression => fileexists("${path}");

  files:
    pass3.file_exists::
      "${path}"
        create        => "false",
        edit_line     => delete_lines_matching("${lines}"),
        edit_defaults => ncf_empty_select("false"),
        classes       => classes_generic("${inner_class_prefix}");

  methods:
    pass3.file_exists::
      "${report_data.method_id}" usebundle => _classes_sanitize("${inner_class_prefix}");
      "${report_data.method_id}" usebundle => _classes_copy("${inner_class_prefix}", "${report_data.method_id}");
      "${report_data.method_id}" usebundle => _classes_copy("${inner_class_prefix}", "${class_prefix}");
      "${report_data.method_id}" usebundle => log_rudder_v4("${path}", "Ensure line(s) '${lines}' absent from ${path}", "");
    pass3.!file_exists::
      "${report_data.method_id}" usebundle => _classes_success("${class_prefix}");
      "${report_data.method_id}" usebundle => _classes_success("${report_data.method_id}");
      "${report_data.method_id}" usebundle => log_rudder_v4("${path}", "File ${path} does not exist - line(s) not present", "");
}
