#####################################################################################
# Copyright 2015 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name Variable iterator from file
# @description Define a variable that will be automatically iterated over
# @documentation The generated variable is a special variable that is automatically
# iterated over. When you call a generic method with this variable as a parameter, n calls will be made,
# one for each items of the variable.
# Note: there is a limit of 10000 items
# Note: empty items are ignored
#
# To use the generated variable, you must use the form `${prefix.name}` with each name replaced with the parameters of this method.
# 
# Be careful that using a global variable can lead to unpredictable content in case of multiple definition, which is implicitly the case when a technique has more than one instance (directive). 
# Please note that only global variables are available within templates.
#
# @parameter prefix  The prefix of the variable name
# @parameter_constraint prefix "regex" : "^(_|[^\p{Punct}\s])+$"
# @parameter name    The variable to define, the full name will be prefix.name
# @parameter_constraint name "regex" : "^(_|[^\p{Punct}\s])+$"
# @parameter file_name        The path to the file
# @parameter separator_regex  Regular expression that is used to split the value into items ( usually: \n )
# @parameter comments_regex   Regular expression that is used to remove comments ( usually: \s*#.*?(?=\n) )
# 
# @parameter_rename variable_prefix prefix
# @parameter_rename variable_name name
# @class_prefix variable_iterator_from_file
# @class_parameter name
# @agent_support = ["cfengine-community"]

bundle agent variable_iterator_from_file(prefix, name, file_name, separator_regex, comments_regex)
{
  vars:
      "old_class_prefix"  string => canonify("variable_iterator_from_file_${name}");
      "args"               slist => { "${prefix}", "${name}", "${file_name}", "${separator_regex}", "${comments_regex}" };
      "report_param"      string => join("_", args);
      "full_class_prefix" string => canonify("variable_iterator_from_file_${report_param}");
      "class_prefix"      string => string_head("${full_class_prefix}", "1000");

      # define the variable within the prefix namespace
      "${prefix}.${name}" slist => readstringlist("${file_name}", "${comments_regex}", "${separator_regex}", "10000", "0");

  classes:
     "variable_defined" expression => isvariable("${prefix}.${name}");

  methods:
    !variable_defined::
      "error"    usebundle => _classes_failure("${old_class_prefix}");
      "error"    usebundle => _classes_failure("${class_prefix}");

    variable_defined::
      "success"  usebundle => _classes_success("${old_class_prefix}");
      "success"  usebundle => _classes_success("${class_prefix}");

    any::
      "report"
        usebundle  => _log_v3("Set the iterator ${prefix}.${name} value to ${file_name} content", "${name}", "${old_class_prefix}", "${class_prefix}", @{args});
}
