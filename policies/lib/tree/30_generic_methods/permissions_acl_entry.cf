#####################################################################################
# Copyright 2019 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name Permissions POSIX acl entry
# @description Verify that an ace is present on a file or directory.
# This method will append the given aces to the current POSIX ACLs of
# the target.
# @documentation The `permissions_*acl_*` manage the POSIX ACL on files and directories.
#
# Please note that the mask will be automatically recalculated when editing ACLs.
#
# #### Parameters
#
# ##### Path
#
# Path can be a regex with the following format:
#
# * `*` matches any filename or directory at one level, e.g. `*.cf` will match all files in one directory that end in .cf but it won't search across directories. `*/*.cf` on the other hand will look two levels deep.
# * `?` matches a single letter
# * `[a-z]` matches any letter from a to z
# * `{x,y,anything}` will match x or y or anything.
#
# ##### Recursive
#
# Can be:
#
# * `true` to apply the given aces to folder and sub-folders and files.
# * or `false` to apply to the strict match of `Path`
#
# If left blank, recursivity will automatically be set to `false`
#
# ##### User and Group
#
# ACE for user and group  can be left blank if they do not need any specification.
# If fulfill, they must respect the format:
#
# `<username|groupname>:<operator><mode>`
#
# with:
#
# * `username` being the Linux account name
# * `groupname` the Linux group name
# * Current `owner user` and `owner group` can be designed by the character `*`
#
# The operator can be:
# * `+` to add the given ACE to the current ones.
# * `-` to remove the given ACE to the current ones.
# * `=` to force the given ACE to the current ones.
#
# You can define multiple ACEs by separating them with commas.
#
# ##### Other
#
# ACE for other must respect the classic:
#
# * `[+-=]r?w?x?`
# It can also be left blank to let the `Other` ACE unchanged.
#
# #### Example
#
# Given a file with the following getfacl output:
#
# ~~~~
# root@server# getfacl /tmp/myTestFile
# getfacl: Removing leading '/' from absolute path names
# # file: tmp/myTestFile
# # owner: root
# # group: root
# user::rwx
# user:bob:rwx
# group::r--
# mask::rwx
# other::---
#
# ~~~~
#
# Applying this method with the following parameters:
#
# * `path`: /tmp/myTestFile
# * `recursive`: false
# * `user`: *:-x, bob:
# * `group`: *:+rw
# * `other`: =r
#
# Will transform the previous ACLs in:
#
# ~~~~
# root@server# getfacl /tmp/myTestFile
# getfacl: Removing leading '/' from absolute path names
# # file: tmp/myTestFile
# # owner: root
# # group: root
# user::rw-
# user:bob:---
# group::rw-
# mask::rw-
# other::r--
#
# ~~~~
#
# This method can not remove a given ACE, see here how the user bob ACE is handled.
#
#
# @parameter path                  Path of the file or directory
# @parameter recursive             Recursive Should ACLs cleanup be recursive, "true" or "false" (defaults to "false")
# @parameter user                  User acls, comma separated, like: bob:+rwx, alice:-w
# @parameter group                 Group acls, comma separated, like: wheel:+wx, anon:-rwx
# @parameter other                 Other acls, like -x
#
# @parameter_constraint recursive "select" : [ "", "true", "false" ]
# @parameter_constraint recursive  "allow_empty_string" : true
# @parameter_constraint user       "allow_empty_string" : true
# @parameter_constraint group      "allow_empty_string" : true
# @parameter_constraint other      "allow_empty_string" : true
#
# @parameter_constraint user       "regex" : "^$|^(([A-z0-9._-]+|\*):([+-=]r?w?x?)?,? *)+$"
# @parameter_constraint group      "regex" : "^$|^(([A-z0-9._-]+|\*):([+-=]r?w?x?)?,? *)+$"
# @parameter_constraint other      "regex" : "^$|^[+-=^]r?w?x?$"
#
# @class_prefix permissions_acl_entry
# @class_parameter path
# @agent_support = ["cfengine-community"]

bundle agent permissions_acl_entry(path, recursive, user, group, other)
{
  vars:
      "class_prefix"    string => canonify("permissions_acl_entry_${path}");
      "user_trimmed"    string => regex_replace("${user}", " ", "", "g");
      "group_trimmed"   string => regex_replace("${group}", " ", "", "g");
      "other_trimmed"   string => regex_replace("${other}", " ", "", "g");

      "user_acls"       slist  => maplist("user:${this}", splitstring("${user_trimmed}",",","99999999999"));
      "group_acls"      slist  => maplist("group:${this}", splitstring("${group_trimmed}",",","99999999999"));
      "other_acls"      string => "all:${other_trimmed}";

      "file_list"                      slist  => findfiles("${path}");
      "file_list_length"               int    => length("file_list");
      "max_range"                      string => execresult("/usr/bin/expr ${file_list_length} - 1", "useshell");
    pass1.!max_range_is_null::
      "indices"                        slist  => expandrange("[0-${max_range}]", 1);
    pass1.max_range_is_null::
      "indices"                        slist  => { "0" };
    pass1::
      "file[${indices}]"               string => nth("file_list", "${indices}");
      "inner_class_prefix[${indices}]" string => canonify("${report_data.method_id}_${indices}");

    pass1.empty_other::
      "applied_aces" slist => { @{user_acls}, @{group_acls} };
    pass1.!empty_other::
      "applied_aces" slist => { @{user_acls}, @{group_acls}, "${other_acls}" };
    pass1::
      "print_aces" string => join("|", "applied_aces");

    pass2::
      "repaired[${indices}]"           string => "repaired: ${file[${indices}]}",
                                           if => "${inner_class_prefix[${indices}]}_repaired";
      "error[${indices}]"              string => "error: ${file[${indices}]}",
                                           if => "${inner_class_prefix[${indices}]}_error";
      "success[${indices}]"            string => "success: ${file[${indices}]}",
                                           if => "${inner_class_prefix[${indices}]}_kept";

      "repaired_list"                  slist  => getvalues("repaired");
      "error_list"                     slist  => getvalues("error");
      "success_list"                   slist  => getvalues("success");

      "report_list"                    slist  => { "@{error_list}", "@{repaired_list}", "@{success_list}" };
      "report_string"                  string => join("$(const.endl)", report_list),
                                       policy => "ifdefined";


  defaults:
      "recursive" string => "false", if_match_regex => "";

  classes:
      "pass3"                  expression => "pass2";
      "pass2"                  expression => "pass1";
      "pass1"                  expression => "any";
      "max_range_is_null"      expression => strcmp("${max_range}", "0");
      "is_recursive"           expression => strcmp("true", "${recursive}");
      "files_found"            expression => isgreaterthan("${file_list_length}", "0");
      "path_exists_${indices}" expression => fileexists("${file[${indices}]}");
      "isdir_${indices}"       expression => isdir("${file[${indices}]}");
      "isrecursive_${indices}" expression => "isdir_${indices}.is_recursive";
      "empty_acls"             expression => and(strcmp("", "${user}"), strcmp("", "${group}"), strcmp("", "${other}"));
      "empty_other"            expression => strcmp("", "${other}");


  files:
    pass2.!empty_acls::
      "${file[${indices}]}"
         depth_search => recurse_with_base("inf"),
                  acl => add_posix_acl(@{applied_aces}),
              classes => classes_generic("${inner_class_prefix[${indices}]}"),
                   if => "path_exists_${indices}.isrecursive_${indices}";

    pass2.!empty_acls::
      "${file[${indices}]}"
            acl => add_posix_acl(@{applied_aces}),
         classes => classes_generic("${inner_class_prefix[${indices}]}"),
              if => "path_exists_${indices}.!isrecursive_${indices}";

  methods:
    !files_found::
      "${report_data.method_id}" usebundle =>  _classes_failure("${class_prefix}");
      "${report_data.method_id}" usebundle =>  _classes_failure("${report_data.method_id}");
      "${report_data.method_id}" usebundle => log_rudder_v4("${path}", "", "");
      "${report_data.method_id}" usebundle => log_rudder_v4("${path}", "No files could be found matching ${path}, add POSIX acl user:${user}, group:${group} and other:${other}", "");

    empty_acls.files_found::
      "${report_data.method_id}" usebundle => _classes_success("${inner_class_prefix[${indices}]}"),
                                        if => "path_exists_${indices}";

    pass2.files_found::
      "${report_data.method_id}" usebundle => _classes_failure("${inner_class_prefix[${indices}]}"),
                                        if => "!path_exists_${indices}";
      "${report_data.method_id}" usebundle => _classes_sanitize("${inner_class_prefix[${indices}]}");

    pass3.files_found::
      "${report_data.method_id}" usebundle => _classes_combine_two("${class_prefix}", "${inner_class_prefix[${indices}]}", "${class_prefix}");
      "${report_data.method_id}" usebundle => _classes_sanitize("${class_prefix}");
      "${report_data.method_id}" usebundle => _classes_copy("${class_prefix}", "${report_data.method_id}");

    is_recursive.pass3.files_found::
      "${report_data.method_id}" usebundle => log_rudder_v4("${path}", "${report_string} ${const.endl} Add POSIX acl user:${user}, group:${group} and other:${other} recursively on ${path}", "");

    !is_recursive.pass3.files_found::
      "${report_data.method_id}" usebundle => log_rudder_v4("${path}", "${report_string} ${const.endl} Add POSIX acl user:${user}, group:${group} and other:${other} on ${path}", "");

}
