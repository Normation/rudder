# Storage bodies

body volume min_free_space(free)
# @brief Warn if the storage doesn't have at least `free` free space.
#
# A warnings is also generated if the storage is smaller than 10K or as
# less than 2 file entries.
#
# @param free Absolute or percentage minimum disk space that should be
# available before warning
{
      check_foreign  => "false";
      freespace      => "$(free)";
      sensible_size  => "10000";
      sensible_count => "2";
}

##

body mount nfs(server,source)
# @brief Mounts the storage at `source` on `server` via nfs protocol.
#
# Also modifies the file system table.
#
# @param server Hostname or IP of remote server
# @param source Path of remote file system to mount
#
# **See also:** `nfs_p()`, `unmount()`
{
      mount_type => "nfs";
      mount_source => "$(source)";
      mount_server => "$(server)";
      edit_fstab => "true";
}

##

body mount nfs_p(server,source,perm)
# @brief Mounts the storage via nfs, with `perm` passed as options to mount.
#
# Also modifies the file system table.
#
# @param server Hostname or IP of remote server
# @param source Path of remote file system to mount
# @param perm A list of options that's passed to the mount command
#
# **See also:** `nfs`, `unmount()`
{
      mount_type => "nfs";
      mount_source => "$(source)";
      mount_server => "$(server)";
      mount_options => {"$(perm)"};
      edit_fstab => "true";
}

##

body mount unmount
# @brief Unmounts the nfs storage.
#
# Also modifies the file system table.
#
# **See also:** `nfs()`, `nfs_p()`
{
      mount_type => "nfs";
      edit_fstab => "true";
      unmount => "true";
}
