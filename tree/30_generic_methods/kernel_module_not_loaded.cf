#####################################################################################
# Copyright 2019 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
######################################################################################

# @name Kernel module not loaded
# @description Ensure that a given kernel module is not loaded on the system
# @documentation Ensure that a given kernel module is not loaded on the system.
#   If the module is loaded, it will try to unload it using modprobe.
#
# @parameter module_name Complete name of the kernel module, as seen by lsmod or listed in /proc/modules
#
# @class_prefix kernel_module_not_loaded
# @class_parameter module_name


bundle agent kernel_module_not_loaded(module_name)
{
  vars:
      "canonified_module_name" string => canonify("${module_name}");
      "old_class_prefix"       string => canonify("variable_module_not_loaded_${module_name}");
      "args"                    slist => { "${module_name}" };
      "report_param"           string => join("_", args);
      "full_class_prefix"      string => canonify("kernel_module_not_loaded_${report_param}");
      "class_prefix"           string => string_head("${full_class_prefix}", "1000");

  classes:
      "pass3" expression => "pass2";
      "pass2" expression => "pass1";
      "pass1" expression => "any";

    pass1.!pass2::
      "module_not_loaded_init_${canonified_module_name}"  not => returnszero("/sbin/lsmod | cut -d ' '  -f 1 | grep -q ^${module_name}$ #Initial check for unloading ${module_name}", "useshell");
    pass1.!pass2.!(global_dry_run|dry_run)::
      "module_unloading_${canonified_module_name}" expression => returnszero("/sbin/modprobe -r ${module_name}", "useshell"),
        ifvarclass => "!module_loaded_init_${canonified_module_name}";
    pass3::
      "module_not_loaded_final_${canonified_module_name}" not => returnszero("/sbin/lsmod | cut -d ' '  -f 1 | grep -q ^${module_name}$ #Final check for unloading ${module_name}", "useshell");

  methods:
    pass3::
      "failure" usebundle => _classes_failure("${old_class_prefix}"),
        ifvarclass => "!module_not_loaded_final_${module_name}";
      "failure" usebundle => _classes_failure("${class_prefix}"),
        ifvarclass => "!module_not_loaded_final_${module_name}";

      "repaired" usebundle => _classes_repaired("${old_class_prefix}"),
        ifvarclass => "!module_not_loaded_init_${canonified_module_name}.module_not_loaded_final_${module_name}";
      "repaired" usebundle => _classes_repaired("${class_prefix}"),
        ifvarclass => "!module_not_loaded_init_${canonified_module_name}.module_not_loaded_final_${module_name}";

      "success" usebundle => _classes_success("${old_class_prefix}"),
        ifvarclass => "module_not_loaded_init_${canonified_module_name}.module_not_loaded_final_${module_name}";
      "success" usebundle => _classes_success("${class_prefix}"),
        ifvarclass => "module_not_loaded_init_${canonified_module_name}.module_not_loaded_final_${module_name}";

      "report"  usebundle => _log_v3("Check that the kernel module ${module_name} is not loaded on the system", "${module_name}", "${old_class_prefix}", "${class_prefix}", @{args});
}
