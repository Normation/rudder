#####################################################################################
# Copyright 2014 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name Service ensure at boot
# @description Force a service to be started at boot
#
# @parameter service_name Service name (as recognized by systemd, init.d, Windows, etc...)
#
# @class_prefix service_ensure_started_at_boot
# @class_parameter service_name
# This bundle will define a class service_ensure_started_at_boot_${canonified_service_name}_{kept,repaired,not_ok,ok,reached}

bundle agent service_ensure_started_at_boot(service_name)
{
  vars:

    systemctl_utility_present::
      "command_to_register" string => "${paths.path[systemctl]} enable ${service_name}.service";

    !systemctl_utility_present.chkconfig_utility_present::
      "command_to_register" string => "${paths.path[chkconfig]} ${service_name} on";
    !systemctl_utility_present.update_rcd_utility_present::
      "command_to_register" string => "${paths.path[update_rcd]} ${service_name} defaults";

    any::

      "canonified_service_name" string => canonify("${service_name}");
      "canonified_command_name" string => canonify("${command_to_register}");

      "old_class_prefix"        string => "service_ensure_started_at_boot_${canonified_service_name}";
      "promisers"          slist => { @{this.callers_promisers}, cf_null }, policy => "ifdefined";
      "class_prefix"      string => canonify(join("_", "promisers"));
      "args"                     slist => { "${service_name}" };

  methods:
    !windows::
      "check_at_boot"
        usebundle => service_check_started_at_boot("${service_name}");

      "define_at_boot"
        usebundle  => command_execution("${service_ensure_started_at_boot.command_to_register}"),
        ifvarclass => "(!has_promiser_stack.service_check_started_at_boot_${canonified_service_name}_not_ok)|(has_promiser_stack.${class_prefix}_check_at_boot_not_ok)";

      "already defined"
        usebundle  => _classes_success("${old_class_prefix}"),
        ifvarclass => "service_check_started_at_boot_${canonified_service_name}_ok";

      "already defined"
        usebundle  => _classes_success("${class_prefix}"),
        ifvarclass => "${class_prefix}_check_at_boot_ok";

      "copy classes"
        usebundle  => _classes_copy("command_execution_${canonified_command_name}", "${old_class_prefix}"),
        ifvarclass => "service_check_started_at_boot_${canonified_service_name}_not_ok";

      "copy classes"
        usebundle  => _classes_copy("${class_prefix}_define_at_boot", "${class_prefix}"),
        ifvarclass => "${class_prefix}_check_at_boot_not_ok";

   any::
      "report"
        usebundle => _log("Ensure service ${service_name} is set at boot", "${old_class_prefix}", "${class_prefix}", @{args}),
        ifvarclass => "(!has_promiser_stack.${old_class_prefix}_reached)|(has_promiser_stack.${class_prefix}_reached)";

  services:
    windows::
      "${service_name}"
        service_policy => "start",
        service_method => bootstart,
        classes        => classes_generic_two("${old_class_prefix}", "${class_prefix}");
}
