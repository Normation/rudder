#####################################################################################
# Copyright 2013 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name File download
# @description Download a file if it does not exit, using curl with a fallback on wget
#
# @parameter source      URL to download from
# @parameter destination File destination
# 
# @class_prefix file_download
# @class_parameter destination
# This bundle will define a class file_download_${destination}_{kept,repaired,not_ok,ok,reached}

bundle agent file_download(source, destination)
{

  vars:

    _stdlib_path_exists_curl::

      "action_command"            string => "${paths.path[curl]} -s -L -o ${destination} ${source}";

    !_stdlib_path_exists_curl._stdlib_path_exists_wget::

      "action_command"            string => "${paths.path[wget]} -O ${destination} ${source}";

    any::

      "canonified_destination"    string => canonify("${destination}");
      "canonified_action_command" string => canonify("${action_command}");

      "old_class_prefix"          string => "file_download_${canonified_destination}";
      "class_prefix"              string => canonify(join("_", "this.callers_promisers"));
      "args"                       slist => { "${source}", "${destination}" };

  classes:

    "file_exists"    expression => fileexists("${destination}");

  methods:

    (_stdlib_path_exists_curl|_stdlib_path_exists_wget).!file_exists::

      "action"                    usebundle => command_execution("${action_command}");

      "class copy"                usebundle => _classes_copy("command_execution_${canonified_action_command}", "${old_class_prefix}");

      "new result classes"        usebundle => _classes_copy("${class_prefix}_action", "${class_prefix}");

      "report"                    usebundle => _logger("Download ${source} into ${destination}", "${old_class_prefix}", "${class_prefix}", @{args}),
                                 ifvarclass => "(!has_promiser_stack.${old_class_prefix}_reached)|(has_promiser_stack.${class_prefix}_reached)";

    !_stdlib_path_exists_curl.!_stdlib_path_exists_wget.!file_exists::

      "force_failure_class"       usebundle => _classes_failure("${old_class_prefix}");
      "new result classes"        usebundle => _classes_failure("${class_prefix}");
      "report"                    usebundle => _logger("Unable to download ${source}: neither wget or curl are installed", "${old_class_prefix}", "${class_prefix}", @{args});

    file_exists::

      "force_success_class"       usebundle => _classes_success("${old_class_prefix}"),
                                 ifvarclass => "!command_execution_${canonified_action_command}_repaired";
      "new result classes"        usebundle => _classes_success("${class_prefix}"),
                                 ifvarclass => "${class_prefix}_action_repaired";
      "report"                    usebundle => _logger("File ${destination} already downloaded", "${old_class_prefix}", "${class_prefix}", @{args}),
                                 ifvarclass => "(!has_promiser_stack.!command_execution_${canonified_action_command}_repaired)|(has_promiser_stack.!${class_prefix}_action_repaired)";

}
