#####################################################################################
# Copyright 2015 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name Condition from expression persistent
# @description Create a new condition that persists across runs
# @documentation This method evaluates an expression, and produces a `${condition_prefix}_true` or a `${condition_prefix}_false` condition depending on the result on the expression, that lasts for the `duration` time:
#
# * If the expression results in a "defined" state, this will produce a kept outcome class and a `${condition_prefix}_true` condition
# * If the expression results in an "undefined" state, this will produce a kept outcome class and a `${condition_prefix}_false` condition
#
# Calling this method with a condition expression transforms a complex expression into a single class condition.
#
# The created condition is global to the agent and is persisted across runs.
# The persistence duration is controlled using `${duration}`; it defines for how long the resulting condition will be defined (in minutes). Note that there is no way to persist indefinitely.
#
# #### Example
#
# If you want to check if a condition evaluates to true, like checking that you are on Monday, 2am, on RedHat systems, and make it last one hour you can use the following policy
#
# ```
# condition_from_expression_persistent_("backup_time", "Monday.redhat.Hr02", "60")
# ```
#
# * If the system is a RedHat like system, on Monday, at 2am, then it will define the following conditions
# `backup_time_true`, `condition_from_expression_persistent_backup_time_kept`, `condition_from_expression_persistent_backup_time_reached`
# * If the system not a RedHat like system, or it's not Monday, or it's not 2am, then it will define the following conditions
# `backup_time_false`, `condition_from_expression_persistent_backup_time_kept`, `condition_from_expression_persistent_backup_time_reached`
# * If the condition is invalid (cannot be parsed), it will define only
# `condition_from_expression_persistent_backup_time_kept`, `condition_from_expression_persistent_backup_time_reached`
#
# @parameter condition_prefix     The condition prefix
# @parameter condition_expression The expression evaluated to create the condition (use 'any' to always evaluate to true)
# @parameter duration             The persistence suffix in minutes
#
# @class_prefix condition_from_expression_persistent
# @class_parameter condition_prefix
 
bundle agent condition_from_expression_persistent(condition_prefix, condition_expression, duration)
{
  vars:
      "old_class_prefix"  string => canonify("condition_from_expression_persistent_${condition_prefix}");
      "args"               slist => { "${condition_prefix}", "${condition_expression}", "${duration}" };
      "report_param"      string => join("_", args);
      "class_prefix"      string => canonify("condition_from_expression_persistent_${report_param}");

  classes:
      "${condition_prefix}_true"   expression => "${condition_expression}",
                                  persistence => "${duration}",
                                        scope => "namespace";

      "${condition_prefix}_false"  expression => "!(${condition_expression})",
                                  persistence => "${duration}",
                                        scope => "namespace";

  methods:
      "success" usebundle => _classes_success("${old_class_prefix}");
      "success" usebundle => _classes_success("${class_prefix}");

      "report"  usebundle => _log_v3("Create the persistent condition ${condition_prefix}_{true,false} with ${condition_expression}", "${condition_prefix}", "${old_class_prefix}", "${class_prefix}", @{args});
}
