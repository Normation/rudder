#####################################################################################
# Copyright 2020 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################
#
# @name Variable string from Augeas 
# @description Use Augeas binaries to call Augtool commands and options to get a node label's value.
#
# @documentation Augeas is a tool that provides an abstraction layer for all the complexities that turn around editing files with regular expressions.
# It's a tree based hierarchy tool, that handle system configuration files where you can securely modify your files. To do so you have to provide 
# the path to the node label's value.
# Augeas uses lenses which are sort of modules that are in charge of identifying and converting files into tree and back. 
# This way, you can manipulate at first the tree and then save your changes into the configuration files on the actual system.
#
# In this method, we introduce using augtool commands and options in order to get the value of a given node's label in the parameters, this can be done 
# by specifying the path to it. The method has in total 5 parameters: variable prefix, variable name, path, lens and file.
#
# Actually there is two ways you can use this method, either you simply provide a path to the node's label as a parameter or you specify a file 
# associated with a lens, then you put the regular path. When you only specify the path to the node's label, your request will includes by default 
# Augeas charging all the lenses and files, on the other hand, if you have a specific file, for example, such as a Json file and you want to associate it 
# to the existing Json lens, then in that case, you need to fill in addition the file and the lens parameter, this way Augeas won't load all its files 
# and lenses except the ones you have specified.
# The generic method will get a node label's value from the agent, otherwise, if Augeas isn't installed on the agent, it will produces an error.
# 
# #### With autoload
#
# Let's consider that you want to obtain the value of the ip address of the first line in the `/etc/hosts` file by indicating the path to it.
# (Note that the `label` and `value` parameters mentioned are naming examples of `variable prefix` and `variable name`, the path `/etc/hosts/1/ipaddr` 
# represents the `ipaddr` node label's value in the first line in the `/etc/hosts`).
#
# ```
# variable_string_from_augeas("label","value","/etc/hosts/1/ipaddr", "", "");
# ```
#
# #### Without autoload
#
# The second case includes two needs of using it, either you want to prevent Augeas from charging all lenses and files while executing your request or 
# you want to associate the `Hosts` lens with the `/etc/hosts` file then get for example the same node value of the first use case.
#
# ```
# variable_string_from_augeas("label","value","/etc/hosts/1/ipaddr","Hosts","/etc/hosts");
# ```
#
# @parameter variable_prefix The prefix of the variable name
# @parameter variable_name   The variable to define, the full name will be variable_prefix.variable_name
# @parameter path            The path to the file and node label
# @parameter lens            The lens specified by the user in case he wants to load a specified lens associated with its file
# @parameter file            The file specified by the user in case he wants to load a specified file associated with its lens
# @parameter_constraint file "allow_empty_string" : true
# @parameter_constraint lens "allow_empty_string" : true
# @class_prefix variable_string_from_augeas
# @class_parameter variable_name


bundle agent variable_string_from_augeas(variable_prefix, variable_name, path, lens, file)
{
 vars:       
     "old_class_prefix" string => canonify("variable_string_from_augeas_${variable_name}");
     "promisers" slist => { @{this.callers_promisers}, cf_null }, policy => "ifdefined";
     "class_prefix" string => canonify(join("_", "promisers"));
     "args" slist => {"${variable_prefix}", "${variable_name}", "${path}", "${lens}", "${file}"};


   pass1.file_defined::
     "case" string => execresult("printf \"set /augeas/load/${lens}/lens \"${lens}.lns\" \n set /augeas/load/${lens}/incl \"${file}\" \n load \n get /files${path}\" | ${ncf_paths.path[augtool]} --noautoload","useshell");
   
   pass1.!file_defined::
     "case" string => execresult("printf \"get /files${path}\" | ${ncf_paths.path[augtool]}","useshell");
   
   pass1::
     "data" data => "{}"; 
     "data" data => data_regextract("= (.*)", "$(case)");
     "${variable_prefix}.${variable_name}" string => "${data[1]}";

 classes:
    "pass2" expression => "pass1";
    "pass1";

   pass2::
     "variable_defined" expression => isvariable("data[1]");  
     "augeas_binaries" expression => fileexists("${ncf_paths.path[augtool]}");

   pass1::
     "file_defined" expression => not(strcmp("${file}",""));

 methods: 
    pass2.augeas_binaries.variable_defined::
       "success" usebundle => _classes_success("${old_class_prefix}");
       "success" usebundle => _classes_success("${class_prefix}");
       "report" usebundle => _log("Getting the node value from ${path}", "${old_class_prefix}", "${class_prefix}", @{args});      

    pass2.augeas_binaries.!variable_defined::
      "failure" usebundle => _classes_failure("${old_class_prefix}");
      "failure" usebundle => _classes_failure("${class_prefix}");
      "report" usebundle => _log("Augeas binaries does exist, but your request has failed. Getting the node value from ${path}","${old_class_prefix}", "${class_prefix}", @{args});
    
    pass2.!augeas_binaries::
      "failure" usebundle => _classes_failure("${old_class_prefix}");
      "failure" usebundle => _classes_failure("${class_prefix}");
      "report" usebundle => _log("Augeas binaries does not exist.", "${old_class_prefix}", "${class_prefix}", @{args});

}
