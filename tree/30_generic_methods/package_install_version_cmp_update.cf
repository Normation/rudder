#####################################################################################
# Copyright 2013 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name Package install version compare (update optional)
# @description Install a package or verify if it is installed in a specific version, or higher or lower version than a version specified, optionally test update or not
#
# @parameter package_name        Name of the package to install or verify
# @parameter version_comparator  Comparator between installed version and defined version, can be ==,<=,>=,<,>,!=
# @parameter package_version     The version of the package to verify (can be "latest" for latest version)
# @parameter action              Action to perform, can be add, verify (defaults to verify)
# @parameter update_policy       While verifying packages, check against latest version ("true") or just installed ("false")
# 
# @class_prefix package_install
# @class_parameter package_name
# This bundle will define a class package_install_${package_name}_{kept,repaired,not_ok,ok,reached}
# 
# Usage example:
# methods:
#     "any" usebundle => package_install_version_cmp_update("postgresql", ">=", "9.1", "verify", "false");

bundle agent package_install_version_cmp_update(package_name, version_comparator, package_version, action, update_policy)
{
  vars:
      "canonified_package_name" string => canonify("${package_name}");
      "old_class_prefix"        string => "package_install_${canonified_package_name}";
      "class_prefix"            string => canonify(join("_", "this.callers_promisers"));
      "args"                     slist => { "${package_name}", "${version_comparator}", "${package_version}", "${action}", "${update_policy}" };

  defaults:
      "action" string => "immediate", if_match_regex => "add";
      "action" string => "warn", if_match_regex => "verify|";
      "update_policy" string => "addupdate", if_match_regex => "true";
      "update_policy" string => "add",       if_match_regex => "false";

  classes:
      "local_package" expression => regcmp("(http:|file:)?/.*", "${package_name}");
      "version_not_specified" expression => strcmp("latest", "${package_version}");
      "immediate" expression => strcmp("immediate", "${action}");
      "warn" expression => strcmp("warn", "${action}");

      "update"    expression => strcmp("addupdate", "${update_policy}");

  packages:

    ## Debian support ##
    !version_not_specified.local_package.debian::
      "${package_name}"
                        package_policy  => "${update_policy}",
                        package_version => "${package_version}",
                        package_select  => "${version_comparator}",
                        package_method  => debian_local_install,
                        classes         => classes_generic_two("${old_class_prefix}", "${class_prefix}"),
                        action          => do_if_immediate;


    !version_not_specified.!local_package.debian::
      "${package_name}"
                        package_policy  => "${update_policy}",
                        package_version => "${package_version}",
                        package_select  => "${version_comparator}",
                        package_method  => apt_get,
                        classes         => classes_generic_two("${old_class_prefix}", "${class_prefix}"),
                        action          => do_if_immediate;


    version_not_specified.local_package.debian::
      "${package_name}"
                        package_policy => "${update_policy}",
                        package_method => debian_local_install,
                        classes        => classes_generic_two("${old_class_prefix}", "${class_prefix}"),
                        action         => do_if_immediate;

    version_not_specified.!local_package.debian::
      "${package_name}"
                        package_policy => "${update_policy}",
                        package_method => apt_get,
                        classes        => classes_generic_two("${old_class_prefix}", "${class_prefix}"),
                        action         => do_if_immediate;

    ## RedHat support ##
    !version_not_specified.local_package.redhat::
      "${package_name}"
                        package_policy  => "${update_policy}",
                        package_version => "${package_version}",
                        package_select  => "${version_comparator}",
                        package_method  => redhat_local_install,
                        classes         => classes_generic_two("${old_class_prefix}", "${class_prefix}"),
                        action          => do_if_immediate;

    !version_not_specified.!local_package.redhat::
      "${package_name}"
                        package_policy  => "${update_policy}",
                        package_version => "${package_version}",
                        package_select  => "${version_comparator}",
                        package_method  => yum_rpm,
                        classes         => classes_generic_two("${old_class_prefix}", "${class_prefix}"),
                        action          => do_if_immediate;


    version_not_specified.local_package.redhat::
      "${package_name}"
                        package_policy => "${update_policy}",
                        package_method => redhat_local_install,
                        classes        => classes_generic_two("${old_class_prefix}", "${class_prefix}"),
                        action         => do_if_immediate;


    version_not_specified.!local_package.redhat::
      "${package_name}"
                        package_policy => "${update_policy}",
                        package_method => yum_rpm,
                        classes        => classes_generic_two("${old_class_prefix}", "${class_prefix}"),
                        action         => do_if_immediate;

    ## SUSE support ##
    !version_not_specified.local_package.SuSE::
      "${package_name}"
                        package_policy  => "${update_policy}",
                        package_version => "${package_version}",
                        package_select  => "${version_comparator}",
                        package_method  => ncf_generic,
                        classes         => classes_generic_two("${old_class_prefix}", "${class_prefix}"),
                        action          => do_if_immediate;

    !version_not_specified.!local_package.SuSE::
      "${package_name}"
                        package_policy  => "${update_policy}",
                        package_version => "${package_version}",
                        package_select  => "${version_comparator}",
                        package_method  => ncf_generic,
                        classes         => classes_generic_two("${old_class_prefix}", "${class_prefix}"),
                        action          => do_if_immediate;


    version_not_specified.local_package.SuSE::
      "${package_name}"
                        package_policy => "${update_policy}",
                        package_method => ncf_generic,
                        classes        => classes_generic_two("${old_class_prefix}", "${class_prefix}"),
                        action         => do_if_immediate;


    version_not_specified.!local_package.SuSE::
      "${package_name}"
                        package_policy => "${update_policy}",
                        package_method => ncf_generic,
                        classes        => classes_generic_two("${old_class_prefix}", "${class_prefix}"),
                        action         => do_if_immediate;

  methods:
    immediate.!update::
      "Reports" usebundle => _logger("Install package ${package_name}", "${old_class_prefix}", "${class_prefix}", @{args});
    immediate.update::
      "Reports" usebundle => _logger("Install or update package ${package_name} in version ${package_version}", "${old_class_prefix}", "${class_prefix}", @{args});
    !immediate.!version_not_specified::
      "Reports" usebundle => _logger("Verify that package ${package_name} is installed in version ${package_version}", "${old_class_prefix}", "${class_prefix}", @{args});
    !immediate.version_not_specified.update::
      "Reports" usebundle => _logger("Verify that package ${package_name} is installed and up-to-date", "${old_class_prefix}", "${class_prefix}", @{args});
    !immediate.version_not_specified.!update::
      "Reports" usebundle => _logger("Verify that package ${package_name} is installed", "${old_class_prefix}", "${class_prefix}", @{args});
}
