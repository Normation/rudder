#####################################################################################
# Copyright 2017 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name Service disabled at boot
# @description Force a service not to be enabled at boot
#
# @parameter service_name Service name (as recognized by systemd, init.d, etc...)
#
# @class_prefix service_disabled
# @class_parameter service_name
# This bundle will define a condition service_disabled_${canonified_service_name}_{kept,repaired,not_ok,ok,reached}

bundle agent service_disabled(service_name)
{
  vars:

    any::

      "canonified_service_name" string => canonify("${service_name}");

      "old_class_prefix"        string => "service_disabled_${canonified_service_name}";
      "promisers"          slist => { @{this.callers_promisers}, cf_null }, policy => "ifdefined";
      "class_prefix"      string => canonify(join("_", "promisers"));
      "args"                     slist => { "${service_name}" };

  methods:
      "disable_reporting"     usebundle => disable_reporting;

    !windows::
      "disable_at_boot"
        usebundle => service_check_disabled_at_boot("${service_name}");

      "undefine_at_boot"
        usebundle => ncf_services("${service_name}", "disable"),
        ifvarclass => "(!has_promiser_stack.service_check_disabled_at_boot_${canonified_service_name}_not_ok)|(has_promiser_stack.${class_prefix}_check_disabled_not_ok)";

      "already undefined"
        usebundle  => _classes_success("${old_class_prefix}"),
        ifvarclass => "service_check_disabled_at_boot_${canonified_service_name}_ok";

      "already undefined"
        usebundle  => _classes_success("${class_prefix}"),
        ifvarclass => "${class_prefix}_check_disabled_ok";

      "copy classes"
        usebundle  => _classes_copy("ncf_services_${canonified_service_name}_disable", "${old_class_prefix}"),
        ifvarclass => "service_check_disabled_at_boot_${canonified_service_name}_not_ok";

      "copy classes"
        usebundle  => _classes_copy("${class_prefix}_disable", "${class_prefix}"),
        ifvarclass => "${class_prefix}_check_disabled_not_ok";

    windows::
      "action"             usebundle => service_stop("${service_name}");

      "class copy"         usebundle => _classes_copy("service_stop_${canonified_service_name}", "${old_class_prefix}"),
                          ifvarclass => "service_stop_${canonified_service_name}_reached";
      "new result classes" usebundle => _classes_copy("${class_prefix}_action", "${class_prefix}"),
                          ifvarclass => "${class_prefix}_action_reached";

   any::
      "enable_reporting"     usebundle => enable_reporting;
      "report"
        usebundle => _log("Ensure service ${service_name} is disabled at boot", "${old_class_prefix}", "${class_prefix}", @{args}),
        ifvarclass => "(!has_promiser_stack.${old_class_prefix}_reached)|(has_promiser_stack.${class_prefix}_reached)";

}
