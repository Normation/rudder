#####################################################################################
# Copyright 2013 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name File lines absent
# @description Ensure that a line is absent in a specific location
#
# @parameter file File name to edit (absolute path on the target node)
#Â @parameter lines Line(s) to remove in the file
#
# @class_prefix file_lines_absent
# @class_parameter file

bundle agent file_lines_absent(file, lines)
{
  vars:
      "old_class_prefix"  string => canonify("file_lines_absent_${file}");
      "args"               slist => { "${file}", "${lines}" };
      "report_param"      string => join("_", args);
      "full_class_prefix" string => canonify("file_lines_absent_${report_param}");
      "class_prefix"      string => string_head("${full_class_prefix}", "1000");

  classes:
      "file_exists"   expression => fileexists("${file}");

  files:
    file_exists::
      "${file}"
        create        => "false",
        edit_line     => delete_lines_matching("${lines}"),
        edit_defaults => ncf_empty_select("false"),
        classes       => classes_generic_two("${old_class_prefix}", "${class_prefix}");

  methods:
    file_exists::
      "sanitize" usebundle => _classes_sanitize("${class_prefix}");
      "sanitize" usebundle => _classes_sanitize("${old_class_prefix}");
      "report"   usebundle => _log_v3("Ensure line(s) '${lines}' absent from ${file}", "${file}", "${old_class_prefix}", "${class_prefix}", @{args});
 
    !file_exists::
      "success"  usebundle => _classes_success("${class_prefix}");
      "success"  usebundle => _classes_success("${old_class_prefix}");
      "report"   usebundle => _log_v3("File ${file} does not exist - line(s) not present", "${file}", "${old_class_prefix}", "${class_prefix}", @{args});


}
