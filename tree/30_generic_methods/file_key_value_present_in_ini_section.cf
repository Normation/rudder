#####################################################################################
# Copyright 2015 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name File key-value in INI section
# @description Ensure that a key-value pair is present in a section in a specific location. The objective of this method is to handle INI-style files.
#
# @parameter path File name to edit (absolute path on the target node)
# @parameter section Name of the INI-style section under which the line should be added or modified (not including the [] brackets)
# @parameter name Name of the key to add or edit
# @parameter value Value of the key to add or edit
#
# @parameter_name path file
#
# @class_prefix file_key_value_present_in_ini_section
# @class_parameter path

bundle agent file_key_value_present_in_ini_section(path, section, name, value)
{
  vars:
      "old_class_prefix"  string => canonify("file_key_value_present_in_ini_section_${path}");
      "args"               slist => { "${path}", "${section}", "${name}", "${value}" };
      "report_param"      string => join("_", args);
      "full_class_prefix" string => canonify("file_key_value_present_in_ini_section_${report_param}");
      "class_prefix"      string => string_head("${full_class_prefix}", "1000");

      "content[${section}][${name}]" string => "${value}";

  classes:
      # check if file is present or not
      # if not, we will create it with the section name and an empty line
      "file_present" expression => fileexists("${file}");

  files:
      "${path}"
        create        => "true",
        edit_line     => insert_lines("[${section}]
"),
        if            => "!file_present";

      "${file}"
        edit_line     => set_variable_values_ini("file_key_value_present_in_ini_section.content", "${section}"),
        edit_defaults => ncf_empty_select("false"),
        classes       => classes_generic_two("${old_class_prefix}", "${class_prefix}"),
        comment       => "Editing file ${path} to set ${name} = ${value} in section ${section}";

  methods:
      "sanitize" usebundle => _classes_sanitize("${class_prefix}");
      "sanitize" usebundle => _classes_sanitize("${old_class_prefix}");
      "report"   usebundle => _log_v3("Set line key=value in section ${section} into ${path}", "${path}", "${old_class_prefix}", "${class_prefix}", @{args});
}

