#####################################################################################
# Copyright 2016 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name Package state
# @agent_version >=3.7
# @description Enforce the state of a package
# @documentation These methods manage packages using a package manager on the system.
#
# `package_present` and `package_absent` use a new package implementation, different from `package_install_*`,
# `package_remove_*` and `package_verify_*`. It should be more reliable, and handle upgrades better.
# It is compatible though, and you can call generic methods from both implementations on the same host.
# The only drawback is that the agent will have to maintain double caches for package lists, which
# may cause a little unneeded overhead.
# 
# #### Setup
#
# If you are using ncf inside Rudder, no specific setup is necessary.
#
# If your are using ncf without Rudder, you need to call the `initialization` bundle
# before using package methods.
# 
# #### Package parameters
# 
# There is only one mandatory parameter, which is the package name to install.
# When it should be installed from a local package, you need to specify the full path to the package as name.
# 
# The version parameter allows specifying a version you want installed.
# It should be the complete versions string as used by the used package manager.
# This parameter allows two special values:
# 
# * *any* which is the default value, and is satisfied by any version of the given package
# * *latest* which will ensure, at each run, that the package is at the latest available version.
# 
# The last parameter is the provider, which is documented in the next section.
#
# You can use [package_state_options](#_package_state_options) to pass options to the underlying package manager
# (currently only with *apt* package manager).
#
# #### Package providers
# 
# This method supports several package managers. You can specify the package manager
# you want to use or let the method choose the default for the local system.
# 
# The package providers include a caching system for package information.
# The package lists (installed, available and available updates) are only updated
# when the cache expires, or when an operation is made by the agent on packages.
#
# *Note*: The implementation of package operations is done in scripts called modules,
# which you can find in `${sys.workdir}/modules/packages/`.
# 
# ##### apt
# 
# This package provider uses *apt*/*dpkg* to manage packages on the system.
# *dpkg* will be used for all local actions, and *apt* is only needed to manage update and
# installation from a repository.
# 
# ##### rpm
# 
# This package provider uses *yum*/*rpm* to manage packages on the system. *rpm* will 
# be used for all local actions, and *yum* is only needed to manage update and
# installation from a repository.
# 
# It is able to downgrade packages when specifying an older version.
#
# ##### zypper
# 
# This package provider uses *zypper*/*rpm* to manage packages on the system.
# *rpm* will be used for all local actions, and *zypper* is only needed to manage update and
# installation from a repository.
#
# Note: If the package version you want to install contains an epoch, you have to specify it
# in the version in the `epoch:version` form, like reported by `zypper info`.
#
# ##### zypper_pattern
#
# This package provider uses zypper with the `-t pattern` option to manage zypper patterns or
# meta-packages on the system.
#
# Since a zypper pattern can be named differently than the rpm package name providing it, please
# always use the exact pattern name (as listed in the output of `zypper patterns`)
# when using this provider.
#
# Note: When installing a pattern from a local rpm file, Rudder assumes that the pattern is built
# following the 
# [official zypper documentation](https://doc.opensuse.org/projects/libzypp/HEAD/zypp-pattern-packages.html).
#
# Older implementations of zypper patterns may not be supported by this module.
#
# ##### slackpkg
#
# This package provider uses Slackware's installpkg and upgradepkg tools to manage 
# packages on the system
#
# ##### pkg
# 
# This package provider uses FreeBSD's *pkg* to manage packages on the system.
# 
# #### Examples
# 
# ```
# # To install postgresql in version 9.1 for x86_64 atchitecture
# package_present("postgresql", "9.1", "x86_64", "");
# # To ensure postgresql is always in the latest available version
# package_present("postgresql", "latest", "", "");
# # To ensure installing postgresql in any version
# package_present("postgresql", "", "", "");
# # To ensure installing postgresql in any version, forcing the yum provider
# package_present("postgresql", "", "", "yum");
# # To ensure installing postgresql from a local package
# package_present("/tmp/postgresql-9.1-1.x86_64.rpm", "", "", "");
# # To remove postgresql
# package_absent("postgresql", "", "", "");
# ```
# 
# See also : [package_present](#_package_present), [package_absent](#_package_absent), [package_state_options](#_package_state_options)
# @parameter name Name of the package, or path to a local package if state is present
# @parameter version Version of the package, can be "latest" for latest version or "any" for any version (defaults to "any")
# @parameter_constraint version "allow_empty_string" : true
# @parameter architecture Architecture of the package, can be an architecture name  or "default" (defaults to "default")
# @parameter_constraint architecture "allow_empty_string" : true
# @parameter provider Package provider to use, can be "yum", "apt", "zypper", "zypper_pattern", "slackpkg", "pkg" or "default" for system default package manager (defaults to "default")
# @parameter_constraint provider "allow_empty_string" : true
# @parameter_constraint provider "select" : [ "", "default", "yum", "apt", "zypper", "zypper_pattern", "slackpkg", "pkg" ]
# @parameter state State of the package, can be "present" or "absent" (defaults to "present")
# @parameter_constraint state "allow_empty_string" : true
# @parameter_constraint state "select" : [ "", "present", "absent" ]
# 
# @class_prefix package_state
# @class_parameter name

bundle agent package_state(name, version, architecture, provider, state)
{
  vars:
    pass1::
      "canonified_name"           string => canonify("${name}");
      "canonified_state"          string => canonify("${state}");

      "old_class_prefix"          string => "package_state_${canonified_name}";

      "args"                      slist  => { "${name}", "${version}", "${architecture}", "${provider}", "${state}" };
      "report_param"              string => join("_", args);
      "full_class_prefix"         string => canonify("package_state_${report_param}");
      "class_prefix"              string => string_head("${full_class_prefix}", "1000");

      # call is to ncf_package("${name}", "${version}", "${architecture}", "${provider}", "${state}", ""), so we have a trailing _ there
      "full_inner_class_prefix"   string => canonify("ncf_package_${report_param}_");
      "inner_class_prefix"        string => string_head("${full_inner_class_prefix}", "1000");

  defaults:
      "state"                     string => "present", if_match_regex => "";

  classes:
      "should_report"         expression => "${report_data.should_report}";

      "pass2" expression => "pass1";
      "pass1" expression => "any";

  methods:
    pass2::
      "disable_reporting_${class_prefix}"
                               usebundle => disable_reporting;

      "action"                 usebundle => ncf_package("${name}", "${version}", "${architecture}", "${provider}", "${state}", "");

      "reenable_reporting_${class_prefix}"
                               usebundle => enable_reporting,
                              ifvarclass => "should_report";
      "class copy"             usebundle => _classes_copy("ncf_package_${canonified_state}_${canonified_name}", "${old_class_prefix}"),
                              ifvarclass => "ncf_package_${canonified_state}_${canonified_name}_reached";
      "new class copy"         usebundle => _classes_copy("${inner_class_prefix}", "${class_prefix}"),
                              ifvarclass => "${inner_class_prefix}_reached";

      # For compatibility with previous version of the generic method
      "class copy compat"      usebundle => _classes_copy("ncf_package_${canonified_state}_${canonified_name}", "package_${canonified_state}_${canonified_name}"),
                              ifvarclass => "ncf_package_${canonified_state}_${canonified_name}_reached";

      "report"                 usebundle => _log_v3("${ncf_package.message}", "${name}", "${old_class_prefix}", "${class_prefix}", @{args});

}
