#####################################################################################
# Copyright 2018 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name File from local source with check
# @description Ensure that a file or directory is copied from a local source if a check command succeeds
# @documentation This method is a conditionnal file copy.
#
# It allows comparing the source and destination, and if they are different, call a command
# with the source file path as argument, and only update the destination if the commands succeeds
# (i.e. returns a code included in rc_ok).
#
# #### Examples
# 
# ```
# # To copy a configuration file only if it passes a config test:
# file_from_local_source_with_check("/tmp/program.conf", "/etc/program.conf", "program --config-test", "0");
# ```
#
# This will:
#
# * Compare `/tmp/program.conf` and `/etc/program.conf`, and return `kept` if files are the same
# * If not, it will execute `program --config-test "/tmp/program.conf"` and check the return code
# * If it is one of the `rc_ok` codes, it will copy `/tmp/program.conf` into `/etc/program.conf` and return a repaired
# * If not, it will return an error
#
# @parameter source      Source file (absolute path on the target node)
# @parameter destination Destination file (absolute path on the target node)
# @parameter check_command   Command to run, it will get the source path as argument
# @parameter rc_ok       Return codes to be considered as valid, separated by a comma (default is 0)
# @parameter_constraint  rc_ok "allow_empty_string" : true
# 
# @class_prefix file_from_local_source_with_check
# @class_parameter destination

bundle agent file_from_local_source_with_check(source, destination, check_command, rc_ok)
{
  vars:
      "old_class_prefix" string => canonify("file_from_local_source_with_check_${destination}");
      "promisers"          slist => { @{this.callers_promisers}, cf_null }, policy => "ifdefined";
      "class_prefix"      string => canonify(join("_", "promisers"));
      "args"              slist => { "${source}", "${destination}", "${check_command}" };
      "canonified_file"   string => canonify("${destination}");

  defaults:
      "rc_ok"             string => "0", if_match_regex => "";

  classes:
    agent::
      "copy_needed" not => returnszero("${paths.path[diff]} \"${source}\" \"${destination}\"", "useshell");

      "should_report"    expression => "${report_data.should_report}";

  methods:
      "disable_reporting_${old_class_prefix}"     usebundle => disable_reporting;
      "check"  usebundle => condition_from_command("file_from_local_source_with_check_${canonified_file}_check", "${check_command} \"${source}\"", "${rc_ok}", "999"),
              ifvarclass => "copy_needed";
      "copy"   usebundle => file_from_local_source("${source}", "${destination}"),
              ifvarclass => "file_from_local_source_with_check_${canonified_file}_check_true";

      "reenable_reporting_${old_class_prefix}"    usebundle => enable_reporting,
                             ifvarclass => "should_report";
      "result" usebundle => _classes_success("${old_class_prefix}"),
              ifvarclass => "!copy_needed";
      "result" usebundle => _classes_failure("${old_class_prefix}"),
                            # here we are not using file_from_local_source_with_check_${canonified_file}_check_false as we want to allow any return code
                            # which is different from rc_ok as failure.
              ifvarclass => "condition_from_command_file_from_local_source_with_check_${canonified_file}_check_failed";
      "result" usebundle => _classes_copy("file_from_local_source_${canonified_file}", "${old_class_prefix}"),
              ifvarclass => "file_from_local_source_with_check_${canonified_file}_check_true";


      "report" usebundle => _log_v3("Copying ${destination} from local ${source} with check", "${destination}", "${old_class_prefix}", "${class_prefix}", @{args}),
              ifvarclass => "!condition_from_command_file_from_local_source_with_check_${canonified_file}_check_failed";
      "report" usebundle => _log_v3("Copying ${destination} from local ${source} failed at check command", "${destination}", "${old_class_prefix}", "${class_prefix}", @{args}),
              ifvarclass => "condition_from_command_file_from_local_source_with_check_${canonified_file}_check_failed";
}
