#####################################################################################
# Copyright 2013 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name File enforce content
# @rename file_content
# @deprecated Use [file_content](#_file_content) instead.
# @description Enforce the content of a file
#
# @parameter file  File name to edit (absolute path on the target node)
# @parameter lines Line(s) to add in the file - if lines is a list, please use @{lines} to pass the iterator rather than iterating over each values
# @parameter enforce Enforce the file to contain only line(s) defined (true or false)
# 
# @class_prefix file_ensure_lines_present
# @class_parameter file

bundle agent file_enforce_content(file, lines, enforce)
{
  vars:
      "canonified_file"    string => canonify("${file}");

      "old_class_prefix" string => canonify("file_ensure_lines_present_${file}");

    # We need to detect if lines is an iterator or a scalar
    !pass1::
      "length_list" int => length("lines");

      # we need to reset the value to 0, otherwise in the next call of this generic method, the previous
      # value will be reused if lines is a scalar
    pass2::
      "length_list" int => "0";

    pass1.!islist::
      "args"              slist => { "${file}", "${lines}", "${enforce}" };

    pass1.islist::
      "args"              slist => { "${file}", "@{lines}", "${enforce}" };

    pass1::
      "report_param"      string => join("_", args);
      "full_class_prefix" string => canonify("file_ensure_lines_present_${report_param}");
      "class_prefix"      string => string_head("${full_class_prefix}", "1000");

      "full_inner_class_prefix" string => canonify("file_lines_present_${report_param}");
      "inner_class_prefix"      string => string_head("${full_inner_class_prefix}", "1000");

  classes:
      "should_report"    expression => "${report_data.should_report}";
    !pass2::
      "islist" expression => isgreaterthan("$(length_list)", "1");

      "pass2" expression => "pass1";
      "pass1" expression => "any";

  methods:
    pass2::
      "disable_reporting_${class_prefix}"
                            usebundle => disable_reporting;
    !islist.pass2::
      "action"              usebundle => file_content("${file}", "${lines}", "${enforce}");
    islist.pass2::
      "action"              usebundle => file_content("${file}", "@{lines}", "${enforce}");
    pass2::
      "reenable_reporting_${class_prefix}"    usebundle => enable_reporting,
                           ifvarclass => "should_report";

      "class copy"          usebundle => _classes_copy("file_lines_present_${canonified_file}", "${old_class_prefix}"),
                           ifvarclass => "file_lines_present_${canonified_file}_reached";
      "new result classes"  usebundle => _classes_copy("${inner_class_prefix}", "${class_prefix}"),
                           ifvarclass => "${inner_class_prefix}_reached";
      "report"              usebundle => _log_v3("Insert content into ${file}", "${file}", "${old_class_prefix}", "${class_prefix}", @{args});

}
