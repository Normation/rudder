#####################################################################################
# Copyright 2017 Normation SAS
#####################################################################################
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, Version 3.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
#####################################################################################

# @name User shell
# @description Define the shell of the user. User must already exist.
# @documentation This method does not create the user.
#   entry example: /bin/false
#
# @parameter login User's login
# @parameter shell User's shell
# 
# @class_prefix user_shell
# @class_parameter login

bundle agent user_shell(login, shell)
{
  vars:
      "old_class_prefix"  string => canonify("user_shell_${login}");
      "args"               slist => { "${login}", "${shell}" };
      "report_param"      string => join("_", args);
      "full_class_prefix" string => canonify("user_shell_${report_param}");
      "class_prefix"      string => string_head("${full_class_prefix}", "1000");

  classes:
      "user_exists" expression => userexists("${login}");
      "args_defined"    expression => "user_exists";

  users:
      "${login}"  policy        => "present",
                  shell         => "${shell}",
                  classes       => classes_generic_two("${old_class_prefix}", "${class_prefix}"),
                  ifvarclass    => "args_defined";

  methods:
    !args_defined::
        "error" usebundle => _classes_failure("${old_class_prefix}");
        "error" usebundle => _classes_failure("${class_prefix}");

        "report_error"
          usebundle => _log_v3("User ${login} does not exist. Setting user ${login} shell to ${shell}", "${login}", "${old_class_prefix}", "${class_prefix}", @{args});

    args_defined::
      "report_success"
        usebundle => _log_v3("User ${login} shell set to ${shell}", "${login}", "${old_class_prefix}", "${class_prefix}", @{args}),
          ifvarclass => "${old_class_prefix}_reached|${class_prefix}_reached";

}

